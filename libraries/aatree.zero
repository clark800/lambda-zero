#* aatree.zero

AADatumType(a) ::= {AADatum(_level : â„•, getValueAA : a)}


define getLevelAA(tree)
    match tree
        Tip -> 0
        Fork(AADatum(n, _), _, _) -> n


define skewAA(tree)
    match tree
        case Tip; Tip
        case Fork(datum @ AADatum(n, _), left, right)
            match left
                case Tip; tree
                case Fork(datum' @ AADatum(n', _), left', right')
                    if n =/= n'
                        tree
                    Fork(datum', left', Fork(datum, right', right))


define splitAA(tree)
    match tree
        case Tip; Tip
        case Fork(datum @ AADatum(n, _), left, right)
            match right
                case Tip; tree
                case Fork(AADatum(n', value'), left', right')
                    if getLevelAA(right') =/= n
                        tree
                    datum' := AADatum(up n', value')
                    Fork(datum', Fork(datum, left, left'), right')


define insertAA((~<), value, tree)
    ss := splitAA <> skewAA
    match tree
        case Tip; Fork(AADatum(1, value), Tip, Tip)
        case Fork(datum @ AADatum(n, value'), left, right)
            if value ~< value'
                if value' ~< value
                    Fork(AADatum(n, value), left, right)
                ss(Fork(datum, insertAA((~<), value, left), right))
            ss(Fork(datum, left, insertAA((~<), value, right)))


define searchAA((~<), value, tree)
    search((~<).on(getValueAA), AADatum(0, value), tree).mapJust(getValueAA)


define flattenAA(tree)
    flatten(tree).map(getValueAA)


define extendAA((~<), values, tree)
    values.fold(insertAA(~<), tree)


define mergeAA((~<), tree', tree)
    extendAA((~<), flattenAA(tree'), tree)

#*
