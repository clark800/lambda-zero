#* evaluate.zero

def runtimeError(message, tag)
    throwError("\nRuntime error: " ++ message, tag)


def expandNumeral(tag, type, n)
    FreeFunction(tag, FreeFunction(tag,
            n |> 0 -> Variable(tag, 2); up n' ->
                Application(tag, Variable(tag, 1), Numeral(tag, type, n'))))


def evaluate(stack, closure @ Closure(term, environment))
    match term
        case Top(tag)
            closure
        case Constant(tag, index)
            if environment.get(index) is Just(closure')
                evaluate(stack, closure')
            closure
        case Variable(_, debruijn)
            if environment.peek(debruijn -- 1) is Just(closure')
                evaluate(stack, closure')
            closure
        case MetaVariable(tag, _)
            if isNil(stack) then closure else runtimeError("metavariable", tag)
        case Function(_, _, _, _, body)
            if stack is argument :: stack'
                evaluate(stack', Closure(body, environment.push(argument)))
            closure
        case Implication(tag, _, _)
            if isNil(stack) then closure else runtimeError("implication", tag)
        case Implicit(tag, _, _)
            if isNil(stack) then closure else runtimeError("implicit", tag)
        case Application(_, applicand, argument)
            evaluate(Closure(argument, environment) :: stack,
                Closure(applicand, environment))
        case Construction(tag, _, _)
            if isNil(stack) then closure else runtimeError("construction", tag)
        case Element(_, _, term')
            evaluate(stack, Closure(term', environment))
        case Numeral(tag, type, n)
            if stack is _ :: _
                numeral := expandNumeral(tag, type, n)
                evaluate(stack, Closure(numeral, environment))
            closure
        case Operation(tag, _, maybeTerm, arity, operator)
            result := operator(evaluate([]), stack.take(arity))
            if result is Just(closure')
                evaluate(stack.drop(arity), closure')
            if maybeTerm is Just(term')
                evaluate(stack, Closure(term', environment))
            runtimeError("operation", tag)


def evaluateClosure(closure)
    evaluate([], closure)


def evaluateTerm(environment, term)
    evaluateClosure(Closure(term, environment))

#*
