#* evaluate.zero

define expandNatural(tag, n)
    metadata := Metadata(Void, Void, [])
    Abstraction(tag, metadata, Abstraction(tag, metadata,
        n |> 0 -> Variable(tag, 2); up n' ->
            Application(tag, False, Variable(tag, 1), Numeral(tag, n'))))


define evaluate(closure @ Closure(term, environment), stack)
    match term
        case Top(tag)
            runtimeError("Top", tag)
        case Variable(_, debruijn)
            with environment.seek(debruijn -- 1) as Just(closure')
                evaluate(closure', stack)
            closure
        case MetaVariable(tag, _)
            runtimeError("MetaVariable", tag)
        case Abstraction(_, _, body)
            with stack as argument :: stack'
                evaluate(Closure(body, environment.push(argument)), stack')
            closure
        case Implication(tag, _, _)
            runtimeError("Implication", tag)
        case Application(_, _, applicand, argument)
            evaluate(Closure(applicand, environment),
                     Closure(argument, environment) :: stack)
        case Construction(tag, _, _, _)
            runtimeError("Construction", tag)
        case Numeral(tag, n)
            with stack as _ :: _
                evaluate(Closure(expandNatural(tag, n), environment), stack)
            closure
        case Operation(_, arity, operator)
            result := operator(`evaluate([]), stack.take(arity))
            evaluate(result, stack.drop(arity))


define evaluateClosure(closure)
    evaluate(closure, [])


define evaluateTerm(term)
    evaluateClosure(Closure(term, []))

#*
