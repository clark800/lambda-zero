#* evaluate.zero

define expandNatural(tag, n)
    metadata := Metadata(Free, Free, [])
    Abstraction(tag, metadata, Abstraction(tag, metadata,
        if n = 0 then Variable(tag, 2) else
            Application(tag, false, Variable(tag, 1), Numeral(tag, n - 1))))


define evaluate(closure @ Closure(term, environment), stack)
    match term
        case Variable(_, debruijn)
            evaluate(environment[debruijn - 1], stack)
        case Abstraction(_, _, body)
            if isNil(stack) then closure else
                evaluate(Closure(body, stack[0] :: environment), tail(stack))
        case Application(_, _, applicand, argument)
            evaluate(Closure(applicand, environment),
                     Closure(argument, environment) :: stack)
        case Numeral(tag, n)
            if isNil(stack) then closure else
                evaluate(Closure(expandNatural(tag, n), environment), stack)
        case Operation(_, arity, operator)
            result := operator(`evaluate([]), stack.take(arity))
            evaluate(result, stack.drop(arity))


define evaluateClosure(closure)
    evaluate(closure, [])


define evaluateTerm(term)
    evaluateClosure(Closure(term, []))

#*
