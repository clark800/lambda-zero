#* opp.zero

LEFT := 0
RIGHT := 1
NEITHER := 2


def getSyntax(operator)
    with operator as Operator(_, _, syntax')
        syntax'
    abort("internal error: getSyntax of non-operator")


isSpecialOperator(op) := isOperator(op) and isSpecial(getSyntax(op))


def reduceTop'(state @ State(stack, operators, onReduce), operator, right)
    with operator as Operator(tag, _, syntax')
        match getFixity(syntax')
            case Prefix(reduce)
                reduction := reduce(operator, right)
                stack' := OperandItem(reduction) :: stack
                state' := State(stack', operators, onReduce)
                onReduce(state', operator, Void, Just(right), reduction)
            case Infix(reduce)
                with stack as leftItem :: stack'
                    with leftItem as OperandItem(left)
                        reduction := reduce(operator, left, right)
                        stack'' := OperandItem(reduction) :: stack'
                        state' := State(stack'', operators, onReduce)
                        onReduce(state', operator,
                            Just(left), Just(right), reduction)
                    pass
                syntaxError("missing left operand", operator)
            case Postfix(reduce)
                syntaxError("internal error: reduceTop'", operator)
            case Openfix(reduce)
                syntaxError("internal error: reduceTop'", operator)
            case Closefix(reduce)
                syntaxError("internal error: reduceTop'", operator)
    syntaxError("internal error: reduceTop'", operator)


def reduceTop(state @ State(stack, operators, onReduce))
    with stack as item :: stack'
        with item as OperandItem(operand)
            with stack' as item' :: stack''
                with item' as OperatorItem(operator)
                    state' := State(stack'', operators, onReduce)
                    reduceTop'(state', operator, operand)
                state
            state
        state
    state


def isHigherPrecedence(left, right)
    with left as Operator(_, leftRank, leftSyntax)
        with right as Operator(_, rightRank, rightSyntax)
            if leftSyntax.getRightPrecedence = rightSyntax.getLeftPrecedence
                if leftSyntax.getAssociativity = NEITHER
                    syntaxError("operator is non-associative", left)

                if rightSyntax.getAssociativity = NEITHER
                    syntaxError("operator is non-associative", right)

                if leftSyntax.getAssociativity =/= rightSyntax.getAssociativity
                    syntaxError("incompatible associativity", right)

                if leftSyntax.getAssociativity = RIGHT
                    leftRank > rightRank
                leftRank >= rightRank

            leftSyntax.getRightPrecedence > rightSyntax.getLeftPrecedence
        False
    False


def shouldReduceTop(operator, state)
    with getStack(state) as item :: stack'
        if isOperatorItem(item)
            False
        with stack' as item' :: _
            with item' as OperatorItem(operator')
                isHigherPrecedence(operator', operator)
            False
        False
    False


def reduceLeft(state, operator)
    while(shouldReduceTop(operator), reduceTop, state)


def parseOperator(tag, value, syntax')
    tag' := renameTag(tag, getAlias(syntax'), getFixityCode(getFixity(syntax')))
    Operator(tag', value, syntax')


def pushSyntax(lexeme, syntax', operators)
    with operators as top :: _
        top.insert(lexeme, syntax') :: operators
    [newDictionary([(lexeme, syntax')])]


def lookupSyntax(lexeme, operators)
    with operators as top :: _
        top.lookup(lexeme)
    Void


def shiftPrefix(State(stack, operators, onReduce), operator)
    State(OperatorItem(operator) :: stack, operators, onReduce)


def shiftPostfix(reduce, State(stack, operators, onReduce), operator)
    with stack as item :: stack'
        with item as OperandItem(operand)
            reduction := reduce(operator, operand)
            stack'' := OperandItem(reduction) :: stack'
            state' := State(stack'', operators, onReduce)
            onReduce(state', operator, Just(operand), Void, reduction)
        pass
    syntaxError("missing left operand for", operator)


def shiftInfix(State(stack, operators, onReduce), operator)
    with stack as top :: _
        if not isOperatorItem(top)
            State(OperatorItem(operator) :: stack, operators, onReduce)
        syntaxError("missing left operand for", operator)
    syntaxError("missing left operand for", operator)


def shiftOpenfix(State(stack, operators, onReduce), open)
    State(OperatorItem(open) :: stack, operators, onReduce)


def reduceBracket(reduceOpen, reduceClose, open, close, before, contents)
    if getBracketType(getSyntax(open)) =/= getBracketType(getSyntax(close))
        if getBracketType(getSyntax(close)) = '\0'
            syntaxError("missing close for", open)
        syntaxError("missing open for", close)
    reduceClose(close, before, reduceOpen(open, before, contents))


def shiftBracket'(reduceOpen, reduceClose, state, open, close, left, right)
    _ @ State(stack, operators, onReduce) := state
    reduction := reduceBracket(
        reduceOpen, reduceClose, open, close, left, right)
    state' := State(OperandItem(reduction) :: stack, operators, onReduce)
    onReduce(state', open, left, right, reduction)


def shiftBracket(reduceOpen, reduceClose, state, open, close, right)
    _ @ State(stack, operators, onReduce) := state
    with stack as leftItem :: stack'
        with leftItem as OperandItem(left)
            state' := State(stack', operators, onReduce)
            shiftBracket'(reduceOpen, reduceClose, state', open, close,
                Just(left), right)
        pass
    shiftBracket'(reduceOpen, reduceClose, state, open, close, Void, right)


def shiftClosefix(reduceClose, State(stack, operators, onReduce), close)
    with stack as item :: stack'
        match item
            case OperatorItem(operator)
                with getFixity(getSyntax(operator)) as Openfix(reduceOpen)
                    state' := State(stack', operators, onReduce)
                    shiftBracket(reduceOpen, reduceClose, state',
                        operator, close, Void)
                syntaxError("missing right operand for", operator)
            case OperandItem(operand)
                with stack' as item' :: stack''
                    with item' as OperatorItem(operator)
                        fixity := getFixity(getSyntax(operator))
                        with fixity as Openfix(reduceOpen)
                            state' := State(stack'', operators, onReduce)
                            shiftBracket(reduceOpen, reduceClose,
                                state', operator, close, Just(operand))
                        pass
                    pass
                syntaxError("missing open for", close)
    syntaxError("missing open for", close)


def getShifter(fixity)
    match fixity
        Prefix(reduce) -> shiftPrefix
        Infix(reduce) -> shiftInfix
        Postfix(reduce) -> shiftPostfix(reduce)
        Openfix(reduce) -> shiftOpenfix
        Closefix(reduce) -> shiftClosefix(reduce)


def shift(state, node)
    state' @ State(stack, operators, onReduce) := reduceLeft(state, node)
    with node as Operator(_, _, syntax')
        getShifter(getFixity(syntax'))(state', node)
    with stack as top :: _
        if not isOperatorItem(top)
            syntaxError("missing operator before", node)
        State(OperandItem(node) :: stack, operators, onReduce)
    syntaxError("missing operator before", node)

#*
