#* shift.zero

StackItem(operandT) ::= {
    OperatorItem(operator : OperatorT(operandT)),
    OperandItem(operand : operandT)
}


def isOperator(item)
    match item
        case OperatorItem(_) -> True
        case OperandItem(_) -> False


StateT(operand) ::= {State(
    stack : List(StackItem(operand)),
    operators : List(TableT(List(â„•), SyntaxT(operand)))
)}

getStack(State(stack, _)) := stack


def isOperandTop(State(stack, operators))
    if stack is top :: _
        not isOperator(top)
    False


def pushOperator(State(stack, operators), operator)
    State(OperatorItem(operator) :: stack, operators)


def pushOperand(State(stack, operators), operand)
    State(OperandItem(operand) :: stack, operators)


def shiftOperand(state, operandTag, operand)
    if isOperandTop(state)
        syntaxError("missing operator before", operandTag)
    pushOperand(state, operand)


def isValidAdfixPrior(getTag, operator, node)
    prior := getPrior(getSyntax(operator))
    isNil(prior) or getTagLexeme(getTag(node)) =*= prior


def isValidInfixPrior(getTag, operator, left, right)
    prior := getPrior(getSyntax(operator))
    if isNil(prior)
        True
    match getAssociativity(getSyntax(operator))
        case LA -> getTagLexeme(getTag(left)) =*= prior
        case RA -> getTagLexeme(getTag(right)) =*= prior
        case NA -> opSyntaxError("invalid operator with prior", operator)


def reduceTop(onReduce, getTag, state, operator, right)
    _ @ State(stack, operators) := state
    match getFixity(getSyntax(operator))
        case Nofix(reduce)
            opSyntaxError("internal error: nofix in reduceTop", operator)
        case Prefix(reduce)
            if not isValidAdfixPrior(getTag, operator, right)
                opSyntaxError("invalid prior for", operator)
            reduction := reduce(getOperatorTag(operator), right)
            state' := pushOperand(state, reduction)
            onReduce(state', operator, Void, Just(right), reduction)
        case Infix(reduce)
            if stack is leftItem :: stack'
                if leftItem is OperandItem(left)
                    if not isValidInfixPrior(getTag, operator, left, right)
                        opSyntaxError("invalid prior for", operator)
                    reduction := reduce(getOperatorTag(operator), left, right)
                    state' := pushOperand(State(stack', operators), reduction)
                    onReduce(state', operator,
                        Just(left), Just(right), reduction)
                pass
            opSyntaxError("missing left operand", operator)
        case _
            opSyntaxError("internal error: reduceTop", operator)


def reduceLeft(onReduce, getTag, state @ State(stack, operators), operator)
    if stack is item :: stack'
        if item is OperandItem(operand)
            if stack' is item' :: stack''
                if item' is OperatorItem(operator')
                    if isHigherPrecedence(operator', operator)
                        state' := reduceTop(onReduce, getTag,
                            State(stack'', operators), operator', operand)
                        reduceLeft(onReduce, getTag, state', operator)
                    state
                state
            state
        state
    state


def shiftPrefix(state, operator)
    if isOperandTop(state)
        opSyntaxError("missing operator before", operator)
    pushOperator(state, operator)


def shiftNofix(reduce, state, operator)
    tag := getOperatorTag(operator)
    shiftOperand(state, tag, reduce(tag))


def shiftPostfix(onReduce, getTag, reduce, State(stack, operators), operator)
    if stack is item :: stack'
        if item is OperandItem(operand)
            if not isValidAdfixPrior(getTag, operator, operand)
                opSyntaxError("invalid prior for", operator)
            reduction := reduce(getOperatorTag(operator), operand)
            state' := pushOperand(State(stack', operators), reduction)
            onReduce(state', operator, Just(operand), Void, reduction)
        pass
    opSyntaxError("missing left operand for", operator)


def shiftInfix(state, operator)
    if not isOperandTop(state)
        opSyntaxError("missing left operand for", operator)
    pushOperator(state, operator)


def reduceBracket(reduceOpen, reduceClose, open, close, before, contents)
    if getBracketType(getSyntax(open)) =/= getBracketType(getSyntax(close))
        if getBracketType(getSyntax(close)) = '\0'
            opSyntaxError("missing close for", open)
        opSyntaxError("missing open for", close)
    openTag := getOperatorTag(open)
    closeTag := getOperatorTag(close)
    reduceClose(closeTag, before, reduceOpen(openTag, before, contents))


def shiftBracket'(onReduce, reduceOpen, reduceClose, state,
        open, close, left, right)
    _ @ State(stack, operators) := state
    operand := reduceBracket(reduceOpen, reduceClose, open, close, left, right)
    state' := pushOperand(state, operand)
    onReduce(state', open, left, right, operand)


def shiftBracket(onReduce, reduceOpen, reduceClose, state, open, close, right)
    _ @ State(stack, operators) := state
    if stack is leftItem :: stack'
        if leftItem is OperandItem(left)
            state' := State(stack', operators)
            shiftBracket'(onReduce, reduceOpen, reduceClose, state',
                open, close, Just(left), right)
        pass
    shiftBracket'(onReduce, reduceOpen, reduceClose, state,
        open, close, Void, right)


def shiftClosefix(onReduce, reduceClose, State(stack, operators), close)
    if stack is item :: stack'
        match item
            case OperatorItem(operator)
                if getFixity(getSyntax(operator)) is Openfix(reduceOpen)
                    state' := State(stack', operators)
                    shiftBracket(onReduce, reduceOpen, reduceClose,
                        state', operator, close, Void)
                opSyntaxError("missing right operand for", operator)
            case OperandItem(operand)
                if stack' is item' :: stack''
                    if item' is OperatorItem(operator)
                        fixity := getFixity(getSyntax(operator))
                        if fixity is Openfix(reduceOpen)
                            state' := State(stack'', operators)
                            shiftBracket(onReduce, reduceOpen, reduceClose,
                                state', operator, close, Just(operand))
                        pass
                    pass
                opSyntaxError("missing open for", close)
    opSyntaxError("missing open for", close)


def getShifter(onReduce, getTag, fixity)
    match fixity
        case Nofix(reduce) -> shiftNofix(reduce)
        case Prefix(reduce) -> shiftPrefix
        case Infix(reduce) -> shiftInfix
        case Postfix(reduce) -> shiftPostfix(onReduce, getTag, reduce)
        case Openfix(reduce) -> pushOperator
        case Closefix(reduce) -> shiftClosefix(onReduce, reduce)


def shiftOperator(onReduce, getTag, state, operator)
    state' := reduceLeft(onReduce, getTag, state, operator)
    fixity := getFixity(getSyntax(operator))
    getShifter(onReduce, getTag, fixity)(state', operator)


def shift(onReduce, getTag, state, item)
    match item
        case OperatorItem(operator)
            shiftOperator(onReduce, getTag, state, operator)
        case OperandItem(operand)
            shiftOperand(state, getTag(operand), operand)


def unshift(state @ State(stack, operators))
    if stack is item :: stack'
        (Just(item), State(stack', operators))
    (Void, state)


def isThisOperator(operator, lexeme)
    getTagLexeme(getOperatorTag(operator)) =*= lexeme


def isThisOperatorItem(item, lexeme)
    if item is OperatorItem(operator)
        isThisOperator(operator, lexeme)
    False


def erase(lexeme, state @ State(stack, operators))
    State(stack.dropIf(`isThisOperatorItem(lexeme)), operators)


def pushOperatorSyntax(lexeme, syntax', operators)
    if operators is top :: _
        top.insert(lexeme, syntax') :: operators
    [newDictionary([(lexeme, syntax')])]


def pushSyntax(lexeme, syntax', state @ State(stack, operators))
    State(stack, pushOperatorSyntax(lexeme, syntax', operators))


def popSyntax(state @ State(stack, operators))
    State(stack, operators.drop(1))


def lookupSyntax(lexeme, State(_, operators))
    if operators is top :: _
        top.lookup(lexeme)
    Void

#*
