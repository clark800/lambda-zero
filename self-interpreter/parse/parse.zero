#* parse.zero

def onReduceHandler(state, operator, left, right, reduction)
    if left is Just(left')
        if left' is Definition(_, type, _, _)
            if type is SyntaxDefinition
                popSyntax(state)
            pass
        pass
    if reduction is Definition(_, type, definiendum, definiens)
        if type is SyntaxDefinition
            state.pushSyntax(getTagLexeme(getTag(definiendum)),
                defineSyntax(state, reduction, definiendum, definiens))
        pass
    state


def shift'(state, item)
    shift(onReduceHandler, getTag, state, item)


def isLeftSectionOperator(operator)
    syntax' := getSyntax(operator)
    if isSpecial(syntax')
        False
    fixity := getFixityCode(getFixity(syntax'))
    fixity = INFIX or fixity = PREFIX


def isRightSectionOperator(operator)
    syntax' := getSyntax(operator)
    if isSpecial(syntax')
        False
    fixity := getFixityCode(getFixity(syntax'))
    fixity = INFIX or fixity = POSTFIX


def shiftOperator''(state, operator)
    # handle section syntax
    (maybeTopItem, state') := unshift(state)
    if maybeTopItem is Just(topItem)
        if topItem is OperatorItem(top)
            if isThisOperator(top, "(") and isRightSectionOperator(operator)
                openTag := renameTag(getOperatorTag(top), "( ", OPENFIX)
                open := state'.parseSymbol(openTag, 0)
                placeholder := OperandItem(FixedName(getOperatorTag(top), ".*"))
                shift'(shift'(shift'(state', open), placeholder),
                    OperatorItem(operator))
            if isThisOperator(operator, ")") and isLeftSectionOperator(top)
                closeTag := renameTag(getOperatorTag(operator), " )", CLOSEFIX)
                close := state.parseSymbol(closeTag, 0)
                placeholder := OperandItem(
                    FixedName(getOperatorTag(operator), "*."))
                shift'(shift'(state, placeholder), close)
            shift'(state, OperatorItem(operator))
        shift'(state, OperatorItem(operator))
    shift'(state, OperatorItem(operator))


def shiftOperator'(state, operator)
    # handle erasure of newlines and semicolons

    if getFixityCode(getFixity(getSyntax(operator))) = CLOSEFIX
        state' := state.erase("\n")
        state'' := if isThisOperator(operator, ")") then
            state'.erase(";") else state'
        shiftOperator''(state'', operator)

    if isThisOperator(operator, "\n")
        if getStack(state) is topItem :: _
            if topItem is OperatorItem(top)
                if getFixityCode(getFixity(getSyntax(top))) = PREFIX
                    opSyntaxError("missing operand after", top)
                state  # ignore newlines after operators
            if getSubprecedence(operator) % 2 =/= 0
                opSyntaxError("odd-width indent after", operator)
            shiftOperator''(state, operator)
        if getSubprecedence(operator) =/= 0
            opSyntaxError("indent on first line", operator)
        state  # ignore newlines at beginning of input

    shiftOperator''(state, operator)


def shiftItem(state, item)
    match item
        OperatorItem(operator) -> shiftOperator'(state, operator)
        OperandItem(_) -> shift'(state, item)


def shiftToken(state, token)
    shiftItem(state, parseToken(state, token))


def parse(getBuiltin, code)
    tokens := lex(code ++ [0]) |: (not) <> isElided
    state := shiftToken(State([], OPERATORS), START)
    stack := getStack(tokens.cascade(shiftToken, state))
    if stack is item :: remaining
        if item is OperandItem(result)
            if not isPreterm(result) and False
                # currently bind has special case errors for non-preterms
                astSyntaxError("parser error", result)  # force syntax errors
            if not isNil(remaining)
                astSyntaxError("parser error", result)
            bindAST(getBuiltin, result)
        pass
    abort "parser error"

#*
