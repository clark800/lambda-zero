#* patterns.zero

def getHead(node)
    if node is Juxtaposition(_, left, _)
        getHead(left)
    node


def getArguments(pattern)
    if pattern is Juxtaposition(_, left, right)
        getArguments(left) ++ [right]
    []


def newProjector(tag, size, index)
    (UnderscoreArrow(tag)^<>(size))(Underscore(tag, size -- index))


def newPatternArrow(pattern, body)
    if pattern is ColonPair(tag, left, right)
        newPatternArrow(left, body).setAnnotation(right)
    if isName(pattern)
        LockedArrow(pattern, body)
    if pattern is AsPattern(tag, left, right)
        newPatternArrow(left, Juxtaposition(tag,
            newPatternArrow(right, body), Underscore(tag, 1)))
    if isJuxtaposition(pattern)
        constructor := getHead(pattern)
        tag := getTag(constructor)
        arguments := getArguments(pattern)
        size := length(arguments)
        underscore := NegativeUnderscore(tag)
        function := arguments.fold(newPatternArrow, body)
        projectors := (0 .. size -- 1).map(newProjector(tag, size))
        projections := projectors.map(Juxtaposition(tag, underscore))
        lockArrow(StrictArrow(tag, [constructor], Underscore(tag, 0),
            projections.cascade(Juxtaposition(tag), function)))
    astSyntaxError("invalid parameter", pattern)


def getNameAndValue(f, left, right)
    if not isPreterm(right)
        astSyntaxError("invalid parameter", right)  # force syntax errors
    if left is Juxtaposition(_, left', right')
        getNameAndValue(f, left', f(right', right))
    (left, right)


def newCaseArrow(left, right)
    if isUnderscore(left)
        SimpleArrow(left, right)
    if isAsPattern(left)
        astSyntaxError("invalid '@' in case", left)
    (name, reconstructor) := getNameAndValue(newPatternArrow, left, right)
    tag := getTag(name)
    constructor := Name(tag)  # name could be a number
    thisName := This(tag)
    body := Juxtaposition(tag, thisName, reconstructor)
    StrictArrow(tag, [constructor], thisName, body)


def addCases(tag, base, extension)
    if extension is Juxtaposition(_, left, right)
        Juxtaposition(tag, addCases(tag, base, left), right)
    base


def newCaseBody(tag, thisName, left, right)
    if left is Arrow(_, type, _, _, _, body)
        base := if type is Simple; Juxtaposition(tag, thisName, body) <| body
        if right is Arrow(_, type', _, _, _, body')
            if type' is Simple
                Juxtaposition(tag, base, body')
            addCases(tag, base, body')
        astSyntaxError("internal error", left)
    astSyntaxError("internal error", left)


def getCaseConstructors(arrow)
    if arrow is Arrow(_, type, constructors, parameter, _, _)
        if type is Simple
            [parameter]
        constructors
    []


def isDefaultCase(arrow)
    if arrow is Arrow(_, type, _, parameter, _, _)
        if type is Simple
            isUnderscore(parameter) or isThisName(parameter, "this")
        False
    False


def getReconstructor(caseArrow)
    if caseArrow is Arrow(_, type, _, _, _, body)
        if type is Simple
            body
        if body is Juxtaposition(_, _, right)
            right
        pass
    astSyntaxError("Internal error: expected case arrow", caseArrow)


def attachDefaultCase(tag, caseArrow, fallback)
    constructorTag := getTag(caseArrow)
    deconstructor := Name(addPrefix(constructorTag, '@'))
    reconstructor := getReconstructor(caseArrow)
    # note: "this" is a positive variable here because we aren't using it
    # as a function as we are in the strict case
    thisName := FixedName(tag, "this")
    body := Juxtaposition(tag, Juxtaposition(tag, Juxtaposition(tag,
        deconstructor, reconstructor), fallback), thisName)
    SimpleArrow(thisName, body)


def combineCases(tag, left, right)
    if isDefaultCase(right)
        attachDefaultCase(tag, left, right)
    thisName := This(tag)
    constructors := getCaseConstructors(left) ++ getCaseConstructors(right)
    body := newCaseBody(tag, thisName, left, right)
    StrictArrow(tag, constructors, thisName, body)

#*
