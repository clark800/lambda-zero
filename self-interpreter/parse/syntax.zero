#* syntax.zero

def reduceApply(tag, left, right)
    Juxtaposition(tag, left, right)


def reduceAdfix(tag, operand)
    reduceApply(tag, Name(tag), operand)


def reduceInfix(tag, left, right)
    reduceApply(tag, reduceApply(tag, Name(tag), left), right)


def reduceArrow(_tag, left, right)
    if isName(left)
        SimpleArrow(left, right)  # might be a case or might not be a case
    if isColonPair(left)
        newPatternArrow(left, right)
    newCaseArrow(left, right)


def reduceDoubleArrow(tag, left, right)
    Arrow(tag, Double, [], left, Void, right)


def reducePipeline(tag, left, right)
    reduceApply(tag, right, left)


def reduceCommaPair(tag, left, right)
    CommaPair(tag, left, right)


def reduceColonPair(tag, left, right)
    if isColonPair(left) or not isValidPattern(left)
        parseError("invalid left side of colon", tag)
    if isColonPair(right) or not isValidPattern(right)
        parseError("invalid right side of colon", tag)
    ColonPair(tag, left, right)


def reduceAsPattern(tag, left, right)
    AsPattern(tag, left, right)


def reduceWhere(tag, left, right)
    if not isDefinition(right)
        parseError("expected definition to right of", tag)
    applyDefinition(right, left)


def reduceWith(tag, asPattern, withBlock)
    with asPattern as AsPattern(_, expression, pattern)
        elseBlock := Underscore(tag, 3)
        caseArrow := newCaseArrow(pattern, withBlock)
        fallback := SimpleArrow(Underscore(tag, 0), elseBlock)
        function := combineCases(tag, caseArrow, fallback)
        LockedArrow(FixedName(tag, "pass"),
            Juxtaposition(tag, function, expression))
    parseError("expected as pattern to right of", tag)


def reduceNewline(tag, left, right)
    with left as Juxtaposition(_, left', right')
        if isThisName(left', "def")
            reduceDefine(tag, right', right)
        if isThisName(left', "case")
            newCaseArrow(right', right)
        if isThisName(left', "with")
            reduceWith(tag, right', right)
        reduceApply(tag, left, right)
    if isDefinition(left)
        applyDefinition(left, right)
    if isCase(left) and isCase(right)
        combineCases(tag, left, right)
    reduceApply(tag, left, right)


def reduceReserved(tag, _left, _right)
    parseError("reserved operator", tag)


def reduceReservedAdfix(tag, _operand)
    parseError("reserved operator", tag)


def reduceClose(_tag, _left, right)
    right


def newOperator'(tag, prior, precedence, associativity, fixity)
    special := not isNil(prior)
    if special and isSameAssociativity(associativity, NA)
        parseError("expected numeric precedence", tag)
    Operator(tag, 0, Syntax(getTagLexeme(tag), precedence, precedence,
        associativity, special, '_', prior, fixity))


def newSyntaxOperator(tag, precedence, fixity, prior)
    p := precedence
    if getLexeme(fixity) =*= "infix"
        newOperator'(tag, prior, p, NA, Infix(reduceInfix))
    if getLexeme(fixity) =*= "infixL"
        newOperator'(tag, prior, p, LA, Infix(reduceInfix))
    if getLexeme(fixity) =*= "infixR"
        newOperator'(tag, prior, p, RA, Infix(reduceInfix))
    if getLexeme(fixity) =*= "interfix"
        newOperator'(tag, prior, p, LA, Infix(reduceApply))
    if getLexeme(fixity) =*= "prefix"
        newOperator'(tag, prior, p, LA, Prefix(reduceAdfix))
    if getLexeme(fixity) =*= "postfix"
        newOperator'(tag, prior, p, LA, Postfix(reduceAdfix))
    syntaxError("invalid fixity", fixity)


def getAliasSyntax(state, name)
    if not isName(name)
        syntaxError("expected operator name", name)
    state.lookupSyntax(getLexeme(name)) ??
        syntaxError("syntax not defined", name)


def newAlias(state, tag, name)
    Operator(tag, 0, getAliasSyntax(state, name))


def copySyntax(state, tag, name)
    lexeme := getTagLexeme(tag)
    match getAliasSyntax(state, name)
        Syntax(_, l, r, a, s, b, p, f) ->
            Operator(tag, 0, Syntax(lexeme, l, r, a, s, b, p, f))


def findPrecedence(state, node)
    syntax' := state.lookupSyntax(getLexeme(node)) ??
        syntaxError("syntax not defined", node)
    if getLeftPrecedence(syntax') =/= getRightPrecedence(syntax')
        syntaxError("operator not supported", node)
    getLeftPrecedence(syntax')


def parsePrecedence(state, node)
    with node as Number(_, precedence)
        if precedence > 99
            syntaxError("invalid precedence", node)
        precedence
    findPrecedence(state, node)


def defineSyntax(state, definition, name, right)
    tag := getTag(definition)
    if not isName(name)
        syntaxError("expected name operand to", definition)
    with right as Juxtaposition(_, fixity, argument)
        if isThisName(fixity, "alias")
            newAlias(state, getTag(name), argument)
        if isThisName(fixity, "syntax")
            copySyntax(state, getTag(name), argument)
        precedence := parsePrecedence(state, argument)
        prior := if isNumber(argument) then "" else getLexeme(argument)
        newSyntaxOperator(getTag(name), precedence, fixity, prior)
    syntaxError("invalid syntax definition", definition)


def addSyntax(state, operator)
    lexeme := getTagLexeme(getOperatorTag(operator))
    if not isVoid(state.lookupSyntax(lexeme))
        oppSyntaxError("syntax already defined", operator)
    state.pushSyntax(lexeme, getSyntax(operator))


def coreAlias(lexeme, aliasName, leftPrecedence, rightPrecedence,
        associativity, fixity)
    (lexeme, Syntax(aliasName, leftPrecedence, rightPrecedence,
        associativity, True, '_', "", fixity))


def coreSyntax(lexeme, leftPrecedence, rightPrecedence, associativity, fixity)
    coreAlias(lexeme, lexeme, leftPrecedence, rightPrecedence,
        associativity, fixity)


def bracketSyntax(lexeme, bracketType, outerPrecedence, fixity)
    fixityCode := getFixityCode(fixity)
    leftPrecedence := if fixityCode = OPENFIX then outerPrecedence else 0
    rightPrecedence := if fixityCode = OPENFIX then 0 else outerPrecedence
    (lexeme, Syntax(lexeme, leftPrecedence, rightPrecedence,
        RA, True, bracketType, "", fixity))


OPERATORS := [newDictionary([
    bracketSyntax("", '\0', 0, Openfix(reduceOpenFile)),
    bracketSyntax("\0", '\0', 0, Closefix(reduceClose)),
    bracketSyntax("(", '(', 95, Openfix(reduceParentheses)),
    bracketSyntax(")", '(', 95, Closefix(reduceClose)),
    bracketSyntax("( ", '(', 95, Openfix(reduceOpenSection)),
    bracketSyntax(" )", '(', 95, Closefix(reduceCloseSection)),
    bracketSyntax("[", '[', 95, Openfix(reduceSquare)),
    bracketSyntax("]", '[', 95, Closefix(reduceClose)),
    bracketSyntax("{", '{', 95, Openfix(reduceCurly)),
    bracketSyntax("}", '{', 95, Closefix(reduceClose)),
    coreSyntax("|", 1, 1, NA, Infix(reduceReserved)),
    coreSyntax(",", 2, 2, LA, Infix(reduceCommaPair)),
    coreSyntax("\n", 3, 3, RA, Infix(reduceNewline)),
    coreSyntax(";;", 4, 4, RA, Infix(reduceNewline)),
    coreSyntax("def", 5, 5, LA, Prefix(reduceAdfix)),
    coreSyntax(":=", 5, 5, RA, Infix(reduceDefine)),
    coreAlias("≔", ":=", 5, 5, RA, Infix(reduceDefine)),
    coreSyntax("::=", 5, 5, RA, Infix(reduceADTDefine)),
    coreAlias("⩴", "::=", 5, 5, RA, Infix(reduceADTDefine)),
    coreSyntax("where", 5, 5, RA, Infix(reduceWhere)),
    coreSyntax("|>", 6, 6, LA, Infix(reducePipeline)),
    coreAlias("⦊", "|>", 6, 6, LA, Infix(reducePipeline)),
    coreSyntax("<|", 6, 6, RA, Infix(reduceApply)),
    coreAlias("⦉","<|", 6, 6, RA, Infix(reduceApply)),
    coreSyntax(";", 8, 8, RA, Infix(reduceNewline)),
    coreSyntax(":", 9, 9, NA, Infix(reduceColonPair)),
    coreSyntax("->", 10, 10, RA, Infix(reduceArrow)),
    coreAlias("↦", "->", 10, 10, RA, Infix(reduceArrow)),
    coreSyntax("=>", 10, 10, RA, Infix(reduceDoubleArrow)),
    coreAlias("⇒", "=>", 10, 10, RA, Infix(reduceDoubleArrow)),
    coreSyntax("case", 11, 11, NA, Prefix(reduceAdfix)),
    coreSyntax("with", 11, 11, NA, Prefix(reduceAdfix)),
    coreSyntax("@", 12, 12, NA, Infix(reduceAsPattern)),
    coreSyntax("as", 12, 12, NA, Infix(reduceAsPattern)),
    coreSyntax("abort", 15, 15, LA, Prefix(reduceAdfix)),
    coreSyntax(".", 92, 92, LA, Infix(reducePipeline)),
    coreSyntax("$", 99, 99, LA, Prefix(reduceReservedAdfix)),
    coreSyntax("syntax", 99, 99, LA, Prefix(reduceAdfix)),
    coreSyntax("alias", 99, 99, LA, Prefix(reduceAdfix))
])]

#*
