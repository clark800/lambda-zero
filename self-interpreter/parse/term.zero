#* term.zero

ClosureT(term) ::= {Closure(
    term : term,
    environment : ArrayT(ClosureT(term))
)}

getTerm(Closure(term, _)) := term
getEnvironment(Closure(_, environment)) := environment

Term ::= {
    Top(tag : TagT),
    Constant(tag : TagT, id : â„•),
    Variable(tag : TagT, debruijn : â„•),
    MetaVariable(tag : TagT, id : â„•),
    Function(tag : TagT, isCase : ð”¹,
        domain : Term, codomain : Term, body : Term),
    Implication(tag : TagT, type : Term, body : Term),
    Implicit(tag : TagT, type : Term, body : Term),
    Application(tag : TagT, applicand : Term, argument : Term),
    Construction(tag : TagT, parameters : List(Term), type : Term),
    Element(tag : TagT, type : Term, term : Term),
    Numeral(tag : TagT, type : Term, value : â„•),
    Operation(tag : TagT, type : Term, arity : â„•, operator :
        ((ClosureT(Term) => ClosureT(Term)) =>
            List(ClosureT(Term)) => ClosureT(Term)))
}


def getTermTag
    Top(tag) -> tag
    Constant(tag, _) -> tag
    Variable(tag, _) -> tag
    MetaVariable(tag, _) -> tag
    Function(tag, _, _, _, _) -> tag
    Implication(tag, _, _) -> tag
    Implicit(tag, _, _) -> tag
    Application(tag, _, _) -> tag
    Construction(tag, _, _) -> tag
    Element(tag, _, _) -> tag
    Numeral(tag, _, _) -> tag
    Operation(tag, _, _, _) -> tag


isTop := Top(_) -> True; _ -> False
isConstant := Constant(_, _) -> True; _ -> False
isVariable := Variable(_, _) -> True; _ -> False
isMetaVariable := MetaVariable(_, _) -> True; _ -> False
isFunction := Function(_, _, _, _, _) -> True; _ -> False
isImplication := Implication(_, _, _) -> True; _ -> False
isImplicit := Implicit(_, _, _) -> True; _ -> False
isApplication := Application(_, _, _) -> True; _ -> False
isConstruction := Construction(_, _, _) -> True; _ -> False
isElement := Element(_, _, _) -> True; _ -> False
isNumeral := Numeral(_, _, _) -> True; _ -> False
isOperation := Operation(_, _, _, _) -> True; _ -> False


def CaseFunction(tag, domain, codomain, body)
    Function(tag, True, domain, codomain, body)


def FreeFunction(tag, body)
    meta := MetaVariable(veil(tag), 0)
    Function(tag, False, meta, meta, body)


def splitSpine(term)
    with term as Application(_, left, right)
        (applicand, arguments) := splitSpine(left)
        (applicand, arguments ++ [right])
    (term, [])


def getTermHead(term)
    first(splitSpine(term))


def maybeAscription(node)
    with node as Element(_, type, _)
        Just(type)
    with node as Numeral(_, type, _)
        Just(type)
    with node as Operation(_, type, _, _)
        Just(type)
    Void


def maybeDebruijnIndex(term)
    with term as Variable(_, debruijn)
        Just(debruijn)
    Void


def maybeBody(term)
    with term as Function(_, _, _, _, body)
        Just(body)
    Void


def maybeApplicand(term)
    with term as Application(_, applicand, _)
        Just(applicand)
    Void


def maybeArgument(term)
    with term as Application(_, _, argument)
        Just(argument)
    Void


def maybeNatural(term)
    with term as Numeral(_, _, value)
        Just(value)
    Void


def maybeAddress(type)
    with type as MetaVariable(_, address)
        Just(address)
    Void


def maybeCount(node)
    with node as Constructor(_, _, _, _, count, _)
        Just(count)
    Void


def maybeConstructorIndex(node)
    with node as Constructor(_, _, _, _, _, index)
        Just(index)
    Void


def maybeHead(term)
    maybe body := maybeBody(term)
    maybe body' := maybeBody(body)
    maybe applicand := maybeApplicand(body')
    maybeArgument(applicand)


def maybeTail(term)
    maybe body := maybeBody(term)
    maybe body' := maybeBody(body)
    maybeArgument(body')


def maybeIsNil(term)
    # returns True for [], False for non-nil list, Void for non-list
    # term should be (n -> p -> n) or (n -> p -> ((p c) cs))

    def ifNil(result)
        maybe body := maybeBody(term)
        maybe body' := maybeBody(body)
        maybe debruijn := maybeDebruijnIndex(body')
        if debruijn = 2 then Just(result) else Void

    def ifPush(result)
        maybe body := maybeBody(term)
        maybe body' := maybeBody(body)
        maybe applicand := maybeApplicand(body')
        maybe applicand' := maybeApplicand(applicand)
        maybe debruijn := maybeDebruijnIndex(applicand')
        if debruijn = 1 then Just(result) else Void

    ifNil(Just(True)) ?? ifPush(Just(False)) ?? Void


def setTag(tag)
    Top(_) -> Top(tag)
    Constant(_, index) -> Constant(tag, index)
    Variable(_, debruijn) -> Variable(tag, debruijn)
    MetaVariable(_, index) -> MetaVariable(tag, index)
    Function(_, isCase, domain, codomain, body) ->
        Function(tag, isCase, domain, codomain, body)
    Implication(_, type, body) -> Implication(tag, type, body)
    Implicit(_, type, body) -> Implicit(tag, type, body)
    Application(_, left, right) -> Application(tag, left, right)
    Construction(_, parameters, type) -> Construction(tag, parameters, type)
    Element(_, type, term) -> Element(tag, type, term)
    Numeral(_, type, value) -> Numeral(tag, type, value)
    Operation(_, type, arity, operator) -> Operation(tag, type, arity, operator)


def setLocation(Tag(_, _, location), term)
    term.setTag(getTermTag(term).setTagLocation(location))


def sameConstant(a, b)
    with a as Constant(_, indexA)
        with b as Constant(_, indexB)
            indexA = indexB
        False
    False


def isSameTerm(a, b)
    match a
        case Top(_)
            isTop(b)
        case Constant(_, index)
            with b as Constant(_, index')
                index = index'
            False
        case Variable(_, debruijn)
            with b as Variable(_, debruijn')
                debruijn = debruijn'
            False
        case MetaVariable(_, index)
            with b as MetaVariable(_, index')
                index = index'
            False
        case Function(_, _, domain, codomain, body)
            with b as Function(_, _, domain', codomain', body')
                isSameTerm(domain, domain') and
                isSameTerm(codomain, codomain') and
                isSameTerm(body, body')
            False
        case Implication(_, type, body)
            with b as Implication(_, type', body')
                isSameTerm(type, type') and isSameTerm(body, body')
            False
        case Implicit(_, type, body)
            with b as Implicit(_, type', body')
                isSameTerm(type, type') and isSameTerm(body, body')
            False
        case Application(_, type, body)
            with b as Application(_, type', body')
                isSameTerm(type, type') and isSameTerm(body, body')
            False
        case Construction(_, parameters, type)
            with b as Construction(_, parameters', type')
                isSameTerm(type, type') and zip(parameters, parameters').all(
                    uncurry(isSameTerm))
            False
        case Element(_, type, term)
            with b as Element(_, type', term')
                isSameTerm(type, type') and isSameTerm(term, term')
            False
        case Numeral(_, _, value)
            with b as Numeral(_, _, value')
                value = value'
            False
        case Operation(tag, _, _, _)
            with b as Operation(tag', _, _, _)
                getTagLexeme(tag) =*= getTagLexeme(tag')
            False


def sameHead(term)
    case Top(_)
        isTop(term)
    case Constant(_, index)
        with term as Constant(_, index')
            index = index'
        False
    case Variable(tag, _)
        False
    case MetaVariable(_, address)
        with term as MetaVariable(_, address')
            address = address'
        False
    case Function(_, _, _, _, _)
        isFunction(term)
    case Implication(_, _, _)
        isImplication(term)
    case Implicit(_, _, _)
        isImplicit(term)
    case Application(_, _, _)
        isApplication(term)
    case Construction(tag, _, _)
        isConstruction(term) and
            getTagLexeme(tag) =*= getTagLexeme(getTermTag(term))
    case Element(_, _, _)
        isElement(term)
    case Numeral(_, _, value)
        with term as Numeral(_, _, value')
            value = value'
        False
    case Operation(tag, _, _, _)
        with term as Operation(tag', _, _, _)
            getTagLexeme(tag) =*= getTagLexeme(tag')
        False


def getComponents
    Top(_) -> []
    Constant(_, _) -> []
    Variable(_, _) -> []
    MetaVariable(_, _) -> []
    Function(_, _, domain, codomain, body) -> [domain, codomain, body]
    Implication(_, type, body) -> [type, body]
    Implicit(_, type, body) -> [type, body]
    Application(_, left, right) -> [left, right]
    Construction(_, parameters, _) -> parameters
    Element(_, type, term) -> [type, term]
    Numeral(_, _, _) -> []
    Operation(_, _, _, _) -> []


def mapComponents(f, term)
    match term
        Top(_) -> term
        Constant(_, _) -> term
        Variable(_, _) -> term
        MetaVariable(_, _) -> term
        Function(tag, isCase, domain, codomain, body) ->
            Function(tag, isCase, f(domain), f(codomain), f(body))
        Implication(tag, type, body) -> Implication(tag, f(type), f(body))
        Implicit(tag, type, body) -> Implicit(tag, f(type), f(body))
        Application(tag, left, right) ->
            Application(tag, mapComponents(f, left), f(right))
        Construction(tag, parameters, type) ->
            Construction(tag, parameters.map(f), type)
        Element(tag, type, term) -> Element(tag, f(type), f(term))
        Numeral(_, _, _) -> term
        Operation(_, _, _, _) -> term


def getSuffix(count, index)
    if count <= 1 or index = 0 then ""
    subscripts := ["â‚€", "â‚", "â‚‚", "â‚ƒ", "â‚„", "â‚…", "â‚†", "â‚‡", "â‚ˆ", "â‚‰"]
    showNatural(index).map(x -> subscripts.at(x -- '0') ?? "").join


def showConstruction(showTerm, headTag, arguments)
    argumentStrings := arguments.map(showTerm)
    with argumentStrings as argumentString :: argumentStrings'
        fixity := getTagFixity(headTag)
        lexeme := getTagLexeme(headTag)
        if fixity = INFIX and length(arguments) = 2
            items := argumentString :: lexeme :: argumentStrings'
            "(" ++ items.joinWith(" ") ++ ")"
        if fixity = PREFIX and length(arguments) = 1
            lexeme ++ "(" ++ argumentString ++ ")"
        if fixity = POSTFIX and length(arguments) = 1
            "(" ++ argumentString ++ ")" ++ lexeme
        showTag(headTag) ++ "(" ++ argumentStrings.joinWith(", ") ++ ")"
    showTag(headTag)


def showApplication(showTerm, applicand, argument)
    (headTerm, applicandArguments) := splitSpine(applicand)
    headTag := getTermTag(headTerm)
    arguments := applicandArguments ++ [argument]
    showConstruction(showTerm, headTag, arguments)


def showMetaVariable(n)
    characters := ['a', 'b', 'c', 'd']
    if n < 4
        [characters.at(n) ?? '#']
    showMetaVariable((n // 4) -- 1) ++ [characters.at(n % 4) ?? '#']


def showTerm
    Top(_) -> "âŠ¤"
    Constant(tag, n) -> showTag(tag) # ++ "$" ++ showNatural(n)
    Variable(tag, n) -> showTag(tag) # ++ "#" ++ showNatural(n)
    MetaVariable(tag, n) -> showMetaVariable(n) # ++ "%" ++ showNatural(n)
    Function(tag, _, _, _, body) ->
        "(" ++ showTag(tag) ++ " â†¦ " ++ showTerm(body) ++ ")"
    Implication(tag, type, body) ->
        if isImplication(type) then
            "(" ++ showTerm(type) ++ ")" ++ " â‡’ " ++ showTerm(body) else
                showTerm(type) ++ " â‡’ " ++ showTerm(body)
    Implicit(tag, type, body) ->
        "(" ++ showTag(tag) ++ " â‡ " ++ showTerm(body) ++ ")"
    Application(_, applicand, argument) ->
        showApplication(showTerm, applicand, argument)
    Construction(tag, parameters, _) ->
        showConstruction(showTerm, tag, parameters)
    Element(tag, _, term) -> showTerm(term)
    Numeral(_, _, n) -> showNatural(n)
    Operation(tag, _, _, _) -> showTag(tag)


def showTermLocation(term)
    showLocation(getTagLocation(getTermTag(term)))


def showTermAndLocation(term)
    showTerm(term) ++ " at " ++ showTermLocation(term)

#*
