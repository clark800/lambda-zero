#* term.zero

ClosureT(term) ::= {Closure(
    term : term,
    environment : ArrayT(ClosureT(term))
)}

getTerm(Closure(term, _)) := term
getEnvironment(Closure(_, environment)) := environment

enclose(term) := Closure(term, newArray([]))

OptimizationT(term) ::= {Optimization(
    arity : ℕ,
    operator : ((ClosureT(term) => ClosureT(term)) =>
        List(ClosureT(term)) => ClosureT(term)?)
)}

Term ::= {
    Unknown(tag : TagT),
    MetaVariable(tag : TagT, id : ℕ),
    Universe(tag : TagT, level : ℕ),
    Variable(tag : TagT, index : ℤ),
    Function(tag : TagT, isCase : 𝔹,
        domain : Term, codomain : Term, body : Term),
    MetaFunction(tag : TagT, type : Term, body : Term),
    Implication(tag : TagT, type : Term, body : Term),
    Application(tag : TagT, applicand : Term, argument : Term),
    Construction(tag : TagT, parameters : List(Term), type : Term,
        signatures : List(Term)),
    Element(tag : TagT, type : Term, term : Term),
    Numeral(tag : TagT, type : Term, value : ℕ),
    Operation(tag : TagT, term : Term, optimization : OptimizationT(Term))
}


def getTermTag
    Unknown(tag) -> tag
    MetaVariable(tag, _) -> tag
    Universe(tag, _) -> tag
    Variable(tag, _) -> tag
    Function(tag, _, _, _, _) -> tag
    MetaFunction(tag, _, _) -> tag
    Implication(tag, _, _) -> tag
    Application(tag, _, _) -> tag
    Construction(tag, _, _, _) -> tag
    Element(tag, _, _) -> tag
    Numeral(tag, _, _) -> tag
    Operation(tag, _, _) -> tag


isUnknown := Unknown(_) -> True; _ -> False
isMetaVariable := MetaVariable(_, _) -> True; _ -> False
isUniverse := Universe(_, _) -> True; _ -> False
isVariable := Variable(_, _) -> True; _ -> False
isFunction := Function(_, _, _, _, _) -> True; _ -> False
isMetaFunction := MetaFunction(_, _, _) -> True; _ -> False
isImplication := Implication(_, _, _) -> True; _ -> False
isApplication := Application(_, _, _) -> True; _ -> False
isConstruction := Construction(_, _, _, _) -> True; _ -> False
isElement := Element(_, _, _) -> True; _ -> False
isNumeral := Numeral(_, _, _) -> True; _ -> False
isOperation := Operation(_, _, _) -> True; _ -> False


def Wildcard(tag)
    MetaFunction(tag, Universe(tag, 0), Variable(tag, -_ 1))


def CaseFunction(tag, domain, codomain, body)
    Function(tag, True, domain, codomain, body)


def FreeFunction(tag, body)
    wildcard := Wildcard(veil(tag))
    Function(tag, False, wildcard, wildcard, body)


def splitSpine(term)
    if term is Application(_, left, right)
        (applicand, arguments) := splitSpine(left)
        (applicand, arguments ++ [right])
    (term, [])


def setTag(tag)
    Unknown(_) -> Unknown(tag)
    MetaVariable(_, index) -> MetaVariable(tag, index)
    Universe(_, level) -> Universe(tag, level)
    Variable(_, index) -> Variable(tag, index)
    Function(_, isCase, domain, codomain, body) ->
        Function(tag, isCase, domain, codomain, body)
    MetaFunction(_, type, body) -> MetaFunction(tag, type, body)
    Implication(_, type, body) -> Implication(tag, type, body)
    Application(_, left, right) -> Application(tag, left, right)
    Construction(_, parameters, type, signatures) ->
        Construction(tag, parameters, type, signatures)
    Element(_, type, term) -> Element(tag, type, term)
    Numeral(_, type, value) -> Numeral(tag, type, value)
    Operation(_, term, op) -> Operation(tag, term, op)


def setLocation(Tag(_, _, location), term)
    term.setTag(getTermTag(term).setTagLocation(location))


def showConstruction(showTerm, headTag, arguments)
    argumentStrings := arguments.map(showTerm)
    if argumentStrings is argumentString :: argumentStrings'
        fixity := getTagFixity(headTag)
        lexeme := getTagLexeme(headTag)
        if fixity = INFIX and length(arguments) = 2
            items := argumentString :: lexeme :: argumentStrings'
            "(" ++ items.joinWith(" ") ++ ")"
        if fixity = PREFIX and length(arguments) = 1
            lexeme ++ "(" ++ argumentString ++ ")"
        if fixity = POSTFIX and length(arguments) = 1
            "(" ++ argumentString ++ ")" ++ lexeme
        showTag(headTag) ++ "(" ++ argumentStrings.joinWith(", ") ++ ")"
    showTag(headTag)


def showApplication(showTerm, applicand, argument)
    (headTerm, applicandArguments) := splitSpine(applicand)
    arguments := applicandArguments ++ [argument]
    if isVariable(headTerm)
        showConstruction(showTerm, getTermTag(headTerm), arguments)
    showTerm(headTerm) ++ "(" ++ arguments.map(showTerm).joinWith(", ") ++ ")"


def showMetaVariable(n)
    characters := ['a', 'b', 'c', 'd']
    if n < 4
        [characters.at(n) ?? '#']
    showMetaVariable((n // 4) -- 1) ++ [characters.at(n % 4) ?? '#']


def showTerm
    Unknown(_) -> "■"
    MetaVariable(tag, n) -> showMetaVariable(n)
    Universe(_, level) -> "⊤" ++ showSubscript(level)
    Variable(tag, index) -> showTag(tag) # ++ "#" ++ showInteger(index)
    Function(tag, _, domain, _, body) ->
        "(" ++ showTag(tag) ++ " : " ++
            showTerm(domain) ++ " ↦ " ++ showTerm(body) ++ ")"
    MetaFunction(tag, type, body) ->
        "(" ++ showTag(tag) ++ " ↝ " ++ showTerm(body) ++ ")"
    Implication(tag, type, body) ->
        if isImplication(type) then
            "(" ++ showTerm(type) ++ ")" ++ " ⇒ " ++ showTerm(body) else
                showTerm(type) ++ " ⇒ " ++ showTerm(body)
    Application(_, applicand, argument) ->
        showApplication(showTerm, applicand, argument)
    Construction(tag, parameters, _, _) ->
        showConstruction(showTerm, tag, parameters)
    Element(tag, _, term) -> showTerm(term)
    Numeral(_, _, n) -> showNatural(n)
    Operation(tag, _, _) -> showTag(tag)


def showTermLocation(term)
    showLocation(getTagLocation(getTermTag(term)))


def showTermAndLocation(term)
    showTerm(term) ++ " at " ++ showTermLocation(term)

#*
