#* tokens.zero

def parseEscapeCode(code, tag)
    if code = '0' then '\0'
    if code = 'n' then '\n'
    if code = 't' then '\t'
    if code = 'r' then '\r'
    if code = '\\' then '\\'
    if code = '\"' then '\"'
    if code = '\'' then '\''
    syntaxError("invalid escape sequence in", tag)


def parseQuotedCharacter(tag, string)
    if string is c :: cs
        if c =/= '\\'
            c
        if cs is c' :: _
            parseEscapeCode(c', tag)
        syntaxError("missing end quote for", tag)
    syntaxError("missing end quote for", tag)


def splitStringLiteral(string)
    if string is c :: cs
        n := if c = '\\' and not isNil(cs) then 2 else 1
        string.take(n) :: splitStringLiteral(string.drop(n))
    []


def getQuoteContents(tag)
    lexeme := getTagLexeme(tag)
    if lexeme.length > 1 and lexeme.reverse.take(1) =*= lexeme.take(1)
        dropLast(lexeme.drop(1))
    syntaxError("missing end quote for", tag)


def getQuotedCharacters(tag)
    splitStringLiteral(getQuoteContents(tag)).map(parseQuotedCharacter(tag))


def parseStringLiteral(tag)
    buildList(tag, getQuotedCharacters(tag).map(Number(tag)))


def parseCharacterLiteral(tag)
    characters := getQuotedCharacters(tag)
    if isNil(characters) or length(characters) > 4
        syntaxError("invalid character literal", tag)
    Number(tag, characters.cascade(n -> c -> 256 * n + c, 0))


def parseNumericLiteral(tag)
    lexeme := getTagLexeme(tag)
    if lexeme.any((not) <> isDigit)
        syntaxError("invalid token", tag)
    maxNatural := "9223372036854775807"
    natural := lexeme.dropWhile((= '0'))
    if natural.length > maxNatural.length
        syntaxError("magnitude of numeral is too large", tag)
    if natural.length = maxNatural.length and
            zipWith((>), natural, maxNatural).any(id)
        syntaxError("magnitude of numeral is too large", tag)
    parsed := parseNatural(lexeme)
    if isVoid(parsed)
        syntaxError("invalid numeral", tag)
    Number(tag, parsed ?? 0)


def parseOperator(tag, value, syntax')
    tag' := renameTag(tag, getAlias(syntax'), getFixityCode(getFixity(syntax')))
    Operator(tag', value, syntax')


def parseSymbol(tag, value, state)
    lexeme := getTagLexeme(tag)
    state.lookupSyntax(lexeme).mapJust(
        OperatorItem <> parseOperator(tag, value)) ??
        OperandItem(Name(tag))


def newOperand(operand)
    if not isPreterm(operand)
        astSyntaxError("invalid operand", operand)  # force syntax errors
    OperandItem(operand)


def parseToken(state, token)
    tag := getTokenTag(token)
    match getTokenCode(token)
        case Space -> abort "internal error: Space reached parseToken"
        case VSpace -> abort "internal error: VSpace reached parseToken"
        case Newline -> state.parseSymbol(renameTag(tag, "\n", INFIX),
            length(getTagLexeme(tag)) -- 1)
        case Symbolic -> state.parseSymbol(tag, 0)
        case Numeric -> newOperand(parseNumericLiteral(tag))
        case Character -> newOperand(parseCharacterLiteral(tag))
        case String -> newOperand(parseStringLiteral(tag))
        case Comment -> abort "internal error: Comment reached parseToken"
        case Invalid -> syntaxError("invalid character", tag)

#*
