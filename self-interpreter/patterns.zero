#* patterns.zero

define getHead(node)
    if isJuxtaposition(node) then getHead(getLeft(node)) else node


define getArguments(pattern)
    if not isJuxtaposition(pattern)
        []
    getArguments(getLeft(pattern)) ++ [getRight(pattern)]


define newProjector(tag, size, index)
    (UnderscoreArrow(tag)^<>(size))(newUnderscore(tag, size - index))


define newPatternArrow(tag, pattern, body)
    if isName(pattern)
        LockedArrow(tag, pattern, body)
    if isAsPattern(pattern)
        label := getLabel(pattern)
        LockedArrow(tag, label, Juxtaposition(tag,
            newPatternArrow(tag, getPattern(pattern), body),
            Reference(getTag(label), 1, 0, getReferenceFixity(label))))
    if isJuxtaposition(pattern)
        constructor := getHead(pattern)
        arguments := getArguments(pattern)
        size := length(arguments)
        underscore := newUnderscore(tag, 1)
        function := arguments.fold(newPatternArrow(tag), body)
        projectors := (0 .. size - 1).map(newProjector(tag, size))
        projections := projectors.map(Juxtaposition(tag, underscore))
        parameter := Name(renameTag(tag, "_"))
        lockArrow(StrictArrow(tag, [constructor], parameter,
            projections.cascade(Juxtaposition(tag), function)))
    syntaxError("invalid parameter", pattern)


define getNameAndValue(f, left, right)
    if isNull(right)
        undefined                   # force syntax errors
    if not isJuxtaposition(left)
        (left, right)
    getNameAndValue(f, getLeft(left), f(getRight(left), right))


define newCaseArrow(tag, left, right)
    pattern := if isAsPattern(left) then getPattern(left) else left
    (name, reconstructor) :=
        getNameAndValue(newPatternArrow(tag), pattern, right)
    constructor := Name(getTag(name))  # name could be a number
    underscore := newUnderscore(tag, 1)
    parameter := Name(renameTag(tag, "_"))
    if not isAsPattern(left)
        StrictArrow(tag, [constructor], parameter,
            Juxtaposition(tag, underscore, reconstructor))
    wrapper := newPatternArrow(tag, getLabel(left), reconstructor)
    argument := Juxtaposition(tag, wrapper, underscore)
    StrictArrow(tag, [constructor], parameter,
        Juxtaposition(tag, underscore, argument))


define addCases(tag, base, extension)
    if not isJuxtaposition(extension)
        base
    merged := addCases(tag, base, getLeft(extension))
    Juxtaposition(tag, merged, getRight(extension))


define newCaseBody(tag, left, right)
    base := if isSimpleArrow(left) then
        Juxtaposition(tag, newUnderscore(tag, 1), getArrowRight(left)) else
        getArrowRight(left)
    if isSimpleArrow(right)
        Juxtaposition(tag, base, getArrowRight(right))
    addCases(tag, base, getArrowRight(right))


define getCaseConstructors(arrow)
    if isSimpleArrow(arrow)
        [getArrowLeft(arrow)]
    getConstructors(arrow)


define combineCases(tag, left, right)
    underscore := Name(renameTag(tag, "_"))
    constructors := getCaseConstructors(left) ++ getCaseConstructors(right)
    StrictArrow(tag, constructors, underscore, newCaseBody(tag, left, right))

#*
