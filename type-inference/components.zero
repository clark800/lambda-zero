#* components.zero

def getComponents
    MetaVariable(_, _) -> []
    Type(_, _) -> []
    Variable(_, _) -> []
    Singleton(_, type, element) -> [type, element]
    Function(_, domain, body) -> [domain, body]
    Implication(_, domain, body) -> [domain, body]
    Intersection(tag, domain, body) ->
        if isConstruction(Intersection(tag, domain, body)) then
            [domain] else [domain, body]
    Application(_, left, right) -> [left, right]
    Numeral(_, _, _) -> []
    Operation(_, _, _) -> []


def mapComponentsWithDepth(f, depth, term)
    match term
        MetaVariable(_, _) -> term
        Type(_, _) -> term
        Variable(_, _) -> term
        Singleton(tag, type, element) ->
            Singleton(tag, type.f(depth), element.f(depth))
        Function(tag, domain, body) ->
            Function(tag, domain.f(depth), body.f(up depth))
        Implication(tag, domain, body) ->
            Implication(tag, domain.f(depth), body.f(up depth))
        Intersection(tag, domain, body) ->
            Intersection(tag, domain.f(depth), body.f(up depth))
        Application(tag, left, right) ->
            Application(tag, left.f(depth), right.f(depth))
        Numeral(_, _, _) -> term
        Operation(_, _, _) -> term


def mapComponents(f, term)
    mapComponentsWithDepth(_ -> f, 0, term)

#*
