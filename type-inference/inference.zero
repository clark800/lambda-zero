#* inference.zero

def getAllFreeVariables(metacontext, metacontext')
    indices := (0 ...)[0, metacontext.getSize]
    join(indices.map(MetaVariable(noTag)).map(
        getFreeVariables(metacontext'))).deduplicateBy(isSameHead)


def generalize(metacontext, oldMetacontext, type)
    # generalize over only metavariables newly introduced in the current
    # definition and leave the rest in the type because they may correspond to
    # the types of local variables that haven't yet been determined
    syntax(\\) := syntax(\)
    vs \\ vs' := vs |: (v -> not vs'.any(isSameHead(v)))
    type' := type.substitute(metacontext)
    universals := type'.getFreeVariables(metacontext) \\
        metacontext.getAllFreeVariables(oldMetacontext)
    Scheme(universals, type')


def instantiate(tag, metacontext, scheme)
    (Scheme(_, type), metacontext') := renameScheme(scheme, metacontext)
    (type.setLocation(tag), metacontext')


def convertToScheme(term, metacontext)
    # note: term must be closed
    if term is MetaFunction(tag, type, body)
        (metavariable, metacontext') := newMetaVariable(tag, metacontext)
        body' := resolve(Closure(body, newArray([enclose(metavariable)])))
        (Scheme(universals, term), metacontext'') :=
            convertToScheme(body', metacontext')
        (Scheme(metavariable :: universals, term), metacontext'')
    (Scheme([], term), metacontext)


def peelMetaFunctions(term, metacontext)
    convertToScheme(term, metacontext).mapFirst(getType)


def getParameterScheme(tag, isCase, domain, metacontext)
    if isCase
        if domain is Construction(_, _, type, _)
            convertToScheme(type, metacontext)
        throwError("expected construction in case annotation", tag)
    (Scheme([], domain), metacontext)


def isWildcard(term)
    if term is MetaFunction(_, _, body)
        if body is Variable(_, index)
            index = 1
        False
    False


def unifyCodomain(codomain, bodyType, environment, metacontext)
    if isWildcard(codomain)
        (bodyType, metacontext)
    metacontext' := unify(metacontext, (
        resolve(Closure(codomain, environment)),
        resolve(Closure(bodyType, environment))))
    if getSize(metacontext') < 0  # todo: remove this when unify returns Either
        abort("force unification errors")
    (codomain, metacontext')


def pushContext(scheme, term, context @ (environment, schemes))
    (environment.push(Closure(term, environment)), schemes.push(scheme))


def infer(context @ (environment, schemes), metacontext)
    case Unknown(tag)
        (Unknown(tag), metacontext)
    case MetaVariable(tag, address)
        # todo: check for type in metacontext
        if metacontext.get(address) ?? Void is Just(term)
            term.infer(context, metacontext)
        newMetaVariable(tag, metacontext)
    case Universe(tag, level)
        (Universe(tag, up level), metacontext)
    case Constant(tag, index)
        scheme := schemes.get(index) ?? throwError("unbound constant", tag)
        scheme.instantiate(tag, metacontext)
    case Variable(tag, index)
        scheme := schemes.peek(index -- 1) ??
            throwError("unbound variable", tag)
        scheme.instantiate(tag, metacontext)
    case Function(tag, isCase, domain, codomain, body)
        # we have to resolve to a closed term before calling peelMetaFunctions
        domain := resolve(evaluateClosure(Closure(domain, environment)))
        (domain, metacontext) := peelMetaFunctions(domain, metacontext)
        domain := resolve(evaluateClosure(Closure(domain, environment)))
        (scheme, metacontext) :=
            getParameterScheme(tag, isCase, domain, metacontext)
        context' := context.pushContext(scheme, Unknown(tag))
        (bodyType, metacontext) := body.infer(context', metacontext)
        (codomain, metacontext) :=
            unifyCodomain(codomain, bodyType, environment, metacontext)
        (Implication(tag, domain, codomain), metacontext)
    case MetaFunction(tag, type, body)
        type' := resolve(evaluateClosure(Closure(type, environment)))
        (metavariable, metacontext') := newMetaVariable(tag, metacontext)
        context' := context.pushContext(Scheme([], type'), metavariable)
        body.infer(context', metacontext')
    case Implication(tag, type, body)
        type' := resolve(evaluateClosure(Closure(type, environment)))
        context' := context.pushContext(Scheme([], type'), Unknown(tag))
        (_, metacontext') := body.infer(context', metacontext)
        (Universe(tag, 0), metacontext')
    case Application(tag, applicand, argument)
        (argumentType, metacontext') := argument.infer(context, metacontext)
        if applicand is Function(_, isCase, domain, codomain, body)
            if not isCase and isMetaVariable(domain)
                # suppport polymorphism in nested functions
                scheme := argumentType.generalize(metacontext', metacontext)
                context' := context.pushContext(scheme, argument)
                body.infer(context', metacontext')
            pass
        (applicandType, metacontext) := applicand.infer(context, metacontext')
        (returnType, metacontext) := metacontext.newMetaVariable(tag)
        applicandTag := getTermTag(applicand)
        functionType := Implication(applicandTag, argumentType, returnType)
        (returnType, unify(metacontext,
            (resolve(Closure(applicandType, environment)),
             resolve(Closure(functionType, environment)))))
    case Construction(tag, parameters, objectType, signatures)
        metacontext' := signatures.cascade(m -> s ->
            second(s.infer(context, m)), metacontext)
        (Universe(tag, 0), metacontext')
    case Element(tag, type, term)
        # type should always be a closed term, so this should be safe
        peelMetaFunctions(type, metacontext)
    case Numeral(tag, type, value)
        (type, metacontext)
    case Operation(tag, type, _, _)
        # type should always be a closed term, so this should be safe
        peelMetaFunctions(type, metacontext)


def getFixScheme(tag)
    syntax(=>>) := syntax(++)
    (=>>) := Implication(tag)
    a := MetaVariable(veil(tag), 0)
    Scheme([a], (a =>> a) =>> a)


def normalize(term)
    # todo: evaluateTerm will return Unknown for variables inside binders
    resolve(evaluateTerm(term)).mapComponents(normalize)


def inferType(schemes, Binding(tag, closure @ Closure(term, environment)))
    if getTagLexeme(tag) =*= "fix"
        schemes.push(getFixScheme(tag))
    metacontext := newArray([])
    context := (environment, schemes)
    (type, metacontext') := term.infer(context, metacontext)
    type' := normalize(resolve(Closure(type, environment)))
    schemes.push(type'.generalize(metacontext', metacontext))


def main(input)
    bindings := reverse(parse(getBuiltin, input))
    schemes := bindings.cascade(inferType, newArray([]))
    tags := bindings.map(getBindingTag)
    showTypings(zip(tags, schemes.toList))

#*
