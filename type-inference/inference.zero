
getDefinitionName(definition) := getParameter(getLeft(definition))
getDefinitionValue(definition) := getRight(definition)
getDefinitionScope(definition) := getBody(getLeft(definition))

newType(tag, code, components) := ((tag, code), components)
newFreeType(tag) := newType(tag, -1, [])
newBooleanType(tag) := newType(tag, -2, [])
newIntegerType(tag) := newType(tag, -3, [])
newFunctionType(tag, left, right) := newType(tag, -4, [left, right])
newTupleType(tag, types) := newType(tag, -5, types)
newDummyTypeVariable(tag, index) := newType(tag, index, [])
newTypeVariable(variables, tag) := (
    type := newDummyTypeVariable(tag, #variables)
    (type, variables.insert(type, newFreeType(tag)))
)
newScheme(universals, type) := (universals, type)

getTypeCode := second <> first
getComponentTypes := second
isCompositeType(type) := length(getComponentTypes(type)) > 0
isTypeVariable(type) := getTypeCode(type) >= 0
isFreeType(type) := getTypeCode(type) = -1
isBooleanType(type) := getTypeCode(type) = -2
isIntegerType(type) := getTypeCode(type) = -3
isFunctionType(type) := getTypeCode(type) = -4
isTupleType(type) := getTypeCode(type) = -5
isFree(type, variables) := !variables.hasKey(type) \/
    isFreeType(variables.get(type, undefined))
isSameType(a, b) := getTypeCode(a) = getTypeCode(b) /\
    zip(getComponentTypes(a), getComponentTypes(b)).all(uncurry(isSameType))
mapComponents(type, f) := newType(getTag(type), getTypeCode(type),
    getComponentTypes(type).map(f))

newBinaryOperatorType(tag) := newFunctionType(tag, newIntegerType(tag),
    newFunctionType(tag, newIntegerType(tag), newIntegerType(tag)))

newComparisonOperatorType(tag) := newFunctionType(tag, newIntegerType(tag),
    newFunctionType(tag, newIntegerType(tag), newBooleanType(tag)))

builtinTypeConstructors := newDictionary([
    ("+", newBinaryOperatorType),
    ("-", newBinaryOperatorType),
    ("*", newBinaryOperatorType),
    ("/", newBinaryOperatorType),
    ("%", newBinaryOperatorType),
    ("=", newComparisonOperatorType),
    ("!=", newComparisonOperatorType),
    ("<", newComparisonOperatorType),
    (">", newComparisonOperatorType),
    ("<=", newComparisonOperatorType),
    (">=", newComparisonOperatorType)
])

knownTypeSchemeConstructors := newDictionary([
    ("true", tag -> newScheme([], newBooleanType(tag))),
    ("false", tag -> newScheme([], newBooleanType(tag))),
    ("if", tag -> (
        (-->) := newFunctionType(tag)
        a := newDummyTypeVariable(tag, 0)
        newScheme([a], newBooleanType(tag) --> (a --> (a --> a)))
    ))
])

getBuiltinType(node) := (
    constructor := builtinTypeConstructors.get(getLexeme(node), undefined)
    constructor(getTag(node))
)

getKnownTypeScheme(name) := (
    constructor := knownTypeSchemeConstructors.get(getLexeme(name),
        tag -> newScheme([], newFreeType(tag)))
    constructor(getTag(name))
)

newVariableMap(mappings) := newTable((<=).on(getTypeCode), mappings)

substitute(type, variables) := (
    isTypeVariable(type) /\ !type.isFree(variables) ?
        variables.get(type, undefined).substitute(variables)
    type.mapComponents(`substitute(variables))
)

// Robinson's Unification Algorithm
// see Comparing Unification Algorithms in First-Order Theorem Proving
// by Krystof Hoder and Andrei Voronkov
unify(variables, (left, right)) := (
    isTypeVariable(left) /\ !left.isFree(variables) ?
        variables.unify((left.substitute(variables), right))
    isTypeVariable(right) /\ !right.isFree(variables) ?
        variables.unify((left, right.substitute(variables)))
    isSameType(left, right) ? variables
    isTypeVariable(left) ? variables.insert(left, right)
    isTypeVariable(right) ? variables.insert(right, left)
    getTypeCode(left) = getTypeCode(right) ?
        zip(getComponentTypes(left), getComponentTypes(right)).
            foldl(unify, variables)
    error("unification error")
)

getFreeVariables(type, variables) := (
    isTypeVariable(type) /\ type.isFree(variables) ?
        [type]
    join(getComponentTypes(type).map(`getFreeVariables(variables)))
)

generalize(type, variables) := newScheme(type.getFreeVariables(variables), type)

newTypeVariables(originals, variables) := originals.map(getTag).
    foldMap(`newTypeVariable, variables)

instantiate(scheme, variables) := (
    (universals, type) := scheme
    (freshVariables, variables') := newTypeVariables(universals, variables)
    substitution := newVariableMap(zip(universals, freshVariables))
    (type.substitute(substitution), variables')
)

// returns (type, variables)
// i think generalize can be done at each reference, but that would duplicate
// work because you would have to search for free type variables again at
// each reference. to prevent this, we can store the free type variables in
// the stack alongside the types.
infer(node, stack, variables) := (
    tag := getTag(node)
    isInteger(node) ? (newIntegerType(tag), variables)
    isBuiltin(node) ? (getBuiltinType(node), variables)
    isTuple(node) ? getTupleElements(node).foldMap(
        `infer(undefined :: stack), variables).mapFirst(newTupleType(tag))
    isReference(node) ? stack[getDebruijnIndex(node)].instantiate(variables)
    isLambda(node) ? (
        (parameterType, variables') := variables.newTypeVariable(tag)
        stack' := newScheme([], parameterType) :: stack
        (bodyType, variables'') := getBody(node).infer(stack', variables')
        (newFunctionType(tag, parameterType, bodyType), variables'')
    )
    isDefinition(node) ? (
        (valueType, variables') := getRight(node).infer(stack, variables)
        stack' := valueType.generalize(variables') :: stack
        getDefinitionScope(node).infer(stack', variables')
    )
    isApplication(node) ? (
        (rightType, variables') := getRight(node).infer(stack, variables)
        (leftType, variables'') := getLeft(node).infer(stack, variables')
        (returnType, variables''') := variables''.newTypeVariable(tag)
        functionType := newFunctionType(getTag(leftType), rightType, returnType)
        (returnType, variables'''.unify((leftType, functionType)))
    )
    undefined
)

inferType(stack, definition) := (
    (name, value) := definition
    knownTypeScheme := getKnownTypeScheme(name)
    !isFreeType(second(knownTypeScheme)) ? knownTypeScheme :: stack
    (type, variables) := infer(value, stack, newVariableMap([]))
    type.substitute(variables).generalize(variables) :: stack
)

// todo: make this work for arbitrarily high index
getVariableName(index) := ['a' + index]

showType(type) := (
    isIntegerType(type) ? "Integer"
    isBooleanType(type) ? "Boolean"
    isTypeVariable(type) ? getVariableName(getTypeCode(type))
    isFunctionType(type) ?
        "(" ++ getComponentTypes(type).map(showType).joinWith(" --> ") ++ ")"
    isTupleType(type) ?
        "(" ++ getComponentTypes(type).map(showType).joinWith("; ") ++ ")"
    undefined
)

showVariableMap(variables) := (
    variables.getElements.map((key, value) ->
        showType(key) ++ " := " ++ showType(value)).joinWith("\n")
)

addLabels(labels, type) := (
    isTypeVariable(type) ? (
        labels.hasKey(type) ? labels
        labels.insert(type, #labels)
    )
    getComponentTypes(type).foldl(addLabels, labels)
)

showTypeWithLabels(type, labels) := (
    isIntegerType(type) ? "Integer"
    isBooleanType(type) ? "Boolean"
    isTypeVariable(type) ? getVariableName(labels.get(type, undefined))
    isFunctionType(type) ?
        "(" ++ getComponentTypes(type).map(
            `showTypeWithLabels(labels)).joinWith(" --> ") ++ ")"
    isTupleType(type) ?
        "(" ++ getComponentTypes(type).map(
            `showTypeWithLabels(labels)).joinWith("; ") ++ ")"
    undefined
)

showPrettyType(type) := (
    labels := newVariableMap([]).addLabels(type)
    showTypeWithLabels(type, labels)
)

newDefinition(definition) :=
    (getDefinitionName(definition), getDefinitionValue(definition))

getDefinitions(node) := (
    !isDefinition(node) ? []
    newDefinition(node) :: getDefinitions(getDefinitionScope(node))
)

showDefinition((name, value)) := getLexeme(name) ++ " := " ++ showAST(value)
showDefinitions(definitions) := definitions.map(showDefinition).joinWith("\n")

showTypings(typings) :=
    typings.map((name, type) ->
        getLexeme(name) ++ " : " ++ showPrettyType(type)).joinWith("\n\n")

main(input) := (
    node := parse(input, "")
    definitions := getDefinitions(node)
    stack := definitions.foldl(inferType, [])
    types := reverse(stack.map(second))
    (names, _) := unzip(definitions)
    showTypings(zip(names, types))
)
