#* inference.zero

define getValue(type, variables)
    with type as TypeVariable(_, address)
        variables.access(address) ?? FreeType
    type


define isFree(variable, variables)
    isFreeType(variables.getValue(variable))


define getFreeVariables(variables, type)
    with type as TypeVariable(_, address)
        type' := variables.access(address) ?? FreeType
        if isFreeType(type')
            [type]
        getFreeVariables(variables, type')
    join(getInternalTypes(type).map(getFreeVariables(variables))).
        deduplicateBy(sameRoot)


define getAllFreeVariables(variables, variables')
    join(variables.dump.map(first).map(TypeVariable(noTerm)).map(
        getFreeVariables(variables'))).deduplicateBy(sameRoot)


define substitute(variables, type)
    with type as TypeVariable(source, address)
        type' := variables.access(address) ?? FreeType
        if isFreeType(type')
            type
        type'' := type'.setSource(source)
        if type''.getFreeVariables(newStore([])).any(sameRoot(type))
            recursionError(type, type, variables)
        type''.substitute(variables)

    type.mapInternalTypes(substitute(variables))


define unifyFailure(originals, variables, (left, right))
    unificationError(originals.mapPair(substitute(variables)),
        left.substitute(variables), right.substitute(variables))


# Robinson's Unification Algorithm
# see Comparing Unification Algorithms in First-Order Theorem Proving
# by Krystof Hoder and Andrei Voronkov
define unify'(originals, variables, (left, right))
    if isTypeVariable(left) and not isFree(left, variables)
        unify'(originals, variables, (variables.getValue(left), right))
    if isTypeVariable(right) and not isFree(right, variables)
        unify'(originals, variables, (left, variables.getValue(right)))
    if isBottomType(left) or isBottomType(right)
        variables
    if sameRoot(left, right)
        zip(getComponentTypes(left), getComponentTypes(right)).
            cascade(unify'(originals), variables)
    with left as TypeVariable(_, address)
        variables.assign(address, right)
    with right as TypeVariable(_, address)
        variables.assign(address, left)
    with left as ADT(_, _, _, _, matchType, _, _)
        if isFreeType(matchType)
            unifyFailure(originals, variables, (left, right))
        unify'(originals, variables, (matchType, right))
    unifyFailure(originals, variables, (left, right))

define unify(variables, types)
    unify'(types, variables, types)

define unifyAll(variables, types)
    with types as type :: types'
        zip(repeat(type), types').cascade(unify, variables)
    variables

define generalize(variables, oldVariables, type)
    syntax(\\) := syntax(\)
    vs \\ vs' := vs |: (v -> not vs'.any(sameRoot(v)))
    type' := type.substitute(variables)
    universals := type'.getFreeVariables(variables) \\
        variables.getAllFreeVariables(oldVariables)
    Scheme(universals, type')

define instantiate(variables, term, Scheme(universals, type))
    (freshVariables, variables') := newTypeVariables(universals, variables)
    addresses := universals.map(maybeAddress).prune
    mappings := zip(addresses, freshVariables)
    renaming := newRenaming(mappings)
    (type.rename(renaming).setSource(term), variables')


define getResultType(type)
    with type as FunctionType(_, _, bodyType)
        getResultType(bodyType)
    type


define convertConstructorType(variable, type)
    if isADT(type)
        variable
    with type as FunctionType(source, parameterType, bodyType)
        FunctionType(source, parameterType,
            convertConstructorType(variable, bodyType))
    abort "Type error: invalid constructor at " ++ showTypeLocation(type)


define constructInnerParameterType(types, variable, term)
    types' := types.map(convertConstructorType(variable))
    types'.fold(FunctionType(term), variable)


define constructParameterType(variables, schemes, term)
    tag := getTermTag(term)
    if isNil(schemes)
        throwError("Type error: no cases:", tag)
    types := schemes.map(getType)
    adtTypes := types.map(getResultType)
    if not adtTypes.all(isADT)
        throwError("Type error: invalid cases:", tag)
    names := deduplicateBy((=*=), adtTypes.map(maybeName).prune)
    counts := deduplicate(adtTypes.map(maybeCount).prune)
    if length(names) =/= 1 or length(counts) =/= 1
        throwError("Type error: inconsistent cases:", tag)
    count := safeHead(counts) ?? 0
    indices := adtTypes.map(maybeIndex).prune
    if length(indices) < count
        throwError("Type error: missing case:", tag)
    if length(indices) > count
        throwError("Type error: too many cases:", tag)
    if not indices =*= sort(indices)
        throwError("Type error: cases out of order:", tag)

    # note: all schemes should have the same universals
    universals := getUniversals(safeHead(schemes) ?? Free)
    variable := TypeVariable(term, length(universals))
    universals' := variable :: universals

    outerType := safeHead(adtTypes) ?? FreeType   # todo: set index to zero
    innerType := constructInnerParameterType(types, variable, term)

    # note: the variables in inner and outer must be instantiated together
    # so we use a trick of packaging them together in a FunctionType
    scheme := Scheme(universals', FunctionType(term, outerType, innerType))
    (polarType, variables') := scheme.instantiate(variables, term)
    with polarType as FunctionType(_, outerType, matchType)
        fullType := setMatchType(outerType, matchType)
        universal := getResultType(matchType)
        fullScheme := Scheme([universal], fullType)
        (outerType, fullScheme, variables')
    throwError("Type error: internal error", tag)


define inferParameterType(stack, variables, metadata, term)
    schemes := getConstructorDebruijnIndices(metadata).map(k ->
        stack.seek(k -- 1)).prune
    if isNil(schemes)
        (parameterType, variables') := variables.newTypeVariable(term)
        (parameterType, Scheme([], parameterType), variables')
    constructParameterType(variables, schemes, term)


# stack is a list of type schemes for all the lambda-bound symbols in scope
# variables is a table that maps type variables to types (either type
#   variables or concrete types); every type variable appears in variables
define infer(stack, variables)
    case term @ Variable(_, debruijn)
        scheme := stack.seek(debruijn -- 1) ?? Free
        scheme.instantiate(variables, term)
    case term @ Abstraction(_, metadata, body)
        if not isFreeType(getType(getAscription(metadata)))
            getAscription(metadata).instantiate(newStore([]), term)
        (type, scheme, variables') :=
            inferParameterType(stack, variables, metadata, term)
        (bodyType, variables'') := infer(stack.push(scheme), variables', body)
        (FunctionType(term, type, bodyType), variables'')
    case term @ Application(tag, isLet, applicand, argument)
        if isLet
            (valueType, variables') := infer(stack, variables, argument)
            stack' := stack.push(valueType.generalize(variables', variables))
            with applicand as Abstraction(_, _, body)
                infer(stack', variables', body)
            infer(stack, variables,
                Application(tag, False, applicand, argument))
        (argumentType, variables') := infer(stack, variables, argument)
        (applicandType, variables'') := infer(stack, variables', applicand)
        (returnType, variables''') := variables''.newTypeVariable(term)
        functionType := FunctionType(noTerm, argumentType, returnType)
        (returnType, unify(variables''', (applicandType, functionType)))
    case term @ Numeral(_, value)
        index := if value = 0 then 1 else 2
        (ADT(term, "â„•", NOFIX, [], FreeType, 2, index), variables)
    case term @ Operation(_, _, _)
        (getBuiltinType(term), variables)


define inferType(stack, definition)
    if getTagLexeme(first(definition)) =*= "fix"
        stack.push(getFixScheme(second(definition)))
    (type, variables') := infer(stack, newStore([]), second(definition))
    stack.push(type.generalize(variables', newStore([])))


define getDefinitions(term)
    with term as Application(_, isLet, applicand, argument)
        if isLet
            with applicand as Abstraction(_, _, body)
                (getTermTag(applicand), argument) :: getDefinitions(body)
            []
        []
    []


define main(input)
    term := parse(input)
    definitions := getDefinitions(term)
    stack := definitions.cascade(inferType, newRandomAccessList([]))
    schemes := stack.getList.reverse
    (names, _) := unzip(definitions)
    showTypings(zip(names, schemes))

#*
