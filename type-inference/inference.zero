#* inference.zero

define pushContext(term, scheme, context @ (terms, schemes))
    # TODO: replace schemes with an array of closures of types
    (terms.push(Closure(term, terms)), schemes.push(scheme))


define pushParameter(scheme, context)
    # TODO: term should be an atom
    pushContext(Top(noTag), scheme, context)


define getAllFreeVariables(metacontext, metacontext')
    indices := (0 ...)[0, metacontext.getSize]
    join(indices.map(MetaVariable(noTag)).map(
        getFreeVariables(metacontext'))).deduplicateBy(sameHead)


define generalize(metacontext, oldMetacontext, type)
    syntax(\\) := syntax(\)
    vs \\ vs' := vs |: (v -> not vs'.any(sameHead(v)))
    type' := type.substitute(metacontext)
    universals := type'.getFreeVariables(metacontext) \\
        metacontext.getAllFreeVariables(oldMetacontext)
    Scheme(universals, type')


define newInstantiationMap(universals, metacontext)
    tags := universals.map(getTermTag)
    (metaVariables, metacontext') := newMetaVariables(tags, metacontext)
    addresses := universals.map(maybeAddress).prune
    renaming := newRenaming(zip(addresses, metaVariables))
    (renaming, metacontext')


define instantiate(tag, metacontext, Scheme(universals, type))
    (mapping, metacontext') := newInstantiationMap(universals, metacontext)
    (type.rename(mapping).setLocation(tag), metacontext')


define peelImplicits(type)
    with type as Binder(tag, variety, _, body)
        if isImplicitV(variety)
            (tags, type') := peelImplicits(body)
            (tag :: tags, type')
        pass
    ([], type)


define instantiateType(metacontext, type)
    # type may be wrapped in some implicits; we create a new metavariable
    # for each implicit, then substitute them in the type
    (tags, type') := peelImplicits(type)
    (metaVariables, metacontext') := newMetaVariables(tags, metacontext)
    environment := newArray(metaVariables.map(v -> Closure(v, newArray([]))))
    (flattenClosure(Closure(type', environment)), metacontext')


define getInnerType(tag, outerType, environment)
    _ @ Closure(construction, environment') :=
        evaluateClosure(Closure(outerType, environment))
    with construction as Construction(_, _, type)
        flattenClosure(Closure(type, environment'))
    throwError("expected construction in case annotation", tag)


define convertToScheme(metacontext, type)
    (tags, type') := peelImplicits(type)
    (metaVariables, metacontext') := newMetaVariables(tags, metacontext)
    environment := newArray(metaVariables.map(v -> Closure(v, newArray([]))))
    type'' := flattenClosure(Closure(type', environment))
    (Scheme(metaVariables, type''), metacontext')


define infer(context @ (terms, types), metacontext)
    case Top(tag)
        (Top(tag), metacontext)
    case Constant(tag, index)
        scheme := types.get(index) ?? throwError("unbound constant", tag)
        scheme.instantiate(tag, metacontext)
    case Variable(tag, debruijn)
        scheme := types.peek(debruijn -- 1) ??
            throwError("unbound variable", tag)
        scheme.instantiate(tag, metacontext)
    case MetaVariable(tag, address)
        with metacontext.get(address) ?? Void as Just(Closure(term, _))
            infer(context, metacontext, term)
        newMetaVariable(tag, metacontext)
    case Binder(tag, variety, type, body)
        match variety
            case AbstractionV
                (parameterType', metacontext') :=
                    if isMetaVariable(type) then
                    metacontext.newMetaVariable(tag) else
                    (type, metacontext)
                context' := context.pushParameter(Scheme([], parameterType'))
                (bodyType, metacontext'') := infer(context', metacontext', body)
                (Implication(tag, parameterType', bodyType), metacontext'')
            case CaseAbstractionV
                (outerType, metacontext') := instantiateType(metacontext, type)
                (innerScheme, metacontext'') := convertToScheme(metacontext',
                    getInnerType(tag, outerType, first(context)))
                context' := context.pushParameter(innerScheme)
                (bodyType, metacontext''') :=
                    infer(context', metacontext'', body)
                (Implication(tag, outerType, bodyType), metacontext''')
            case InstanceV
                instantiateType(metacontext, type)
            case ImplicationV
                (parameterType, metacontext') :=
                    infer(context, metacontext, type)
                context' := context.pushParameter(Scheme([], parameterType))
                (bodyType, metacontext'') := infer(context', metacontext', body)
                (Top(veil(tag)), metacontext'')
            case ImplicitV
                context' := context.pushParameter(Scheme([], type))
                infer(context', metacontext, body)
    case Application(tag, applicand, argument)
        (argumentType, metacontext') := infer(context, metacontext, argument)
        with applicand as Binder(_, variety, type, body)
            if isAbstractionV(variety)
                context' := context.pushParameter(
                    argumentType.generalize(metacontext', metacontext))
                infer(context', metacontext', body)
            pass
        (applicandType, metacontext'') :=
            infer(context, metacontext', applicand)
        (returnType, metacontext''') := metacontext''.newMetaVariable(tag)
        applicandTag := getTermTag(applicand)
        functionType := Implication(applicandTag, argumentType, returnType)
        (returnType, unify(context, metacontext''',
            (applicandType, functionType)))
    case Construction(tag, parameters, objectType)
        (Top(veil(tag)), metacontext)
    case Numeral(tag, type, value)
        (type, metacontext)
    case Operation(tag, type, _, _)
        instantiateType(metacontext, type)


define getFixScheme(tag)
    syntax(>->) := syntax(++)
    (>->) := Implication(tag)
    a := MetaVariable(veil(tag), 0)
    Scheme([a], (a >-> a) >-> a)


define inferType(context, (tag, term))
    if getTagLexeme(tag) =*= "fix"
        context.pushContext(term, getFixScheme(getTermTag(term)))
    metacontext := newArray([])
    (type, metacontext') := infer(context, metacontext, term)
    scheme := type.generalize(metacontext', metacontext)
    context.pushContext(term, scheme)


define main(input)
    (tags, _, environment) := parse(input)
    terms := environment.toList.map(getTerm)
    context := (newArray([]), newArray([]))
    context' := zip(tags, terms).cascade(inferType, context)
    schemes := second(context').toList
    showTypings(zip(tags, schemes))

#*
