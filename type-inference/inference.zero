#* inference.zero

def getAllFreeVariables(metacontext, metacontext')
    indices := (0 ...)[0, metacontext.getSize]
    join(indices.map(MetaVariable(noTag)).map(
        getFreeVariables(metacontext'))).deduplicateBy(isSameHead)


def generalize(metacontext, oldMetacontext, type)
    # generalize over only metavariables newly introduced in the current
    # definition and leave the rest in the type because they may correspond to
    # the types of local variables that haven't yet been determined
    syntax(\\) := syntax(\)
    vs \\ vs' := vs |: (v -> not vs'.any(isSameHead(v)))
    type' := type.substitute(metacontext)
    universals := type'.getFreeVariables(metacontext) \\
        metacontext.getAllFreeVariables(oldMetacontext)
    Scheme(universals, type')


def instantiate(tag, scheme)
    Scheme(_, type) <- renameScheme(scheme)
    lift type.setLocation(tag)


def convertToScheme(term)
    # note: term must be closed
    if term is MetaFunction(tag, type, body)
        metavariable <- newMetaVariable(tag)
        body' := resolve(Closure(body, newArray([enclose(metavariable)])))
        Scheme(universals, term) <- convertToScheme(body')
        lift Scheme(metavariable :: universals, term)
    lift Scheme([], term)


def peelMetaFunctions(term)
    Scheme(_, term) <- convertToScheme(term)
    lift term


def getParameterScheme(tag, isCase, domain)
    if isCase
        if domain is Construction(_, _, type, _)
            convertToScheme(type)
        throwError("expected construction in case annotation", tag)
    lift Scheme([], domain)


def isWildcard(term)
    if term is MetaFunction(_, _, body)
        if body is Variable(_, index)
            index = 1
        False
    False


def unifyCodomain(codomain, bodyType, environment)
    if isWildcard(codomain)
        lift bodyType
    _ <- unify(resolve(Closure(codomain, environment)),
              resolve(Closure(bodyType, environment)))
    metacontext <- getState
    if getSize(metacontext) < 0  # todo: remove this when unify returns Either
        abort("force unification errors")
    lift codomain


def pushContext(scheme, term, context @ (environment, schemes))
    (environment.push(Closure(term, environment)), schemes.push(scheme))


def infer(context @ (environment, schemes))
    case Unknown(tag)
        lift Unknown(tag)
    case MetaVariable(tag, address)
        # todo: check for type in metacontext
        metacontext <- getState
        if metacontext.get(address) ?? Void is Just(term)
            term.infer(context)
        newMetaVariable(tag)
    case Universe(tag, level)
        lift Universe(tag, up level)
    case Constant(tag, index)
        scheme := schemes.get(index) ?? throwError("unbound constant", tag)
        scheme.instantiate(tag)
    case Variable(tag, index)
        scheme := schemes.peek(index -- 1) ??
            throwError("unbound variable", tag)
        scheme.instantiate(tag)
    case Function(tag, isCase, domain, codomain, body)
        # we have to resolve to a closed term before calling peelMetaFunctions
        domain := resolve(evaluateClosure(Closure(domain, environment)))
        domain <- peelMetaFunctions(domain)
        domain := resolve(evaluateClosure(Closure(domain, environment)))
        scheme <- getParameterScheme(tag, isCase, domain)
        context' := context.pushContext(scheme, Unknown(tag))
        bodyType <- body.infer(context')
        codomain <- unifyCodomain(codomain, bodyType, environment)
        lift Implication(tag, domain, codomain)
    case MetaFunction(tag, type, body)
        type' := resolve(evaluateClosure(Closure(type, environment)))
        metavariable <- newMetaVariable(tag)
        context' := context.pushContext(Scheme([], type'), metavariable)
        body.infer(context')
    case Implication(tag, type, body)
        type' := resolve(evaluateClosure(Closure(type, environment)))
        context' := context.pushContext(Scheme([], type'), Unknown(tag))
        _ <- body.infer(context')
        lift Universe(tag, 0)
    case Application(tag, applicand, argument)
        metacontext <- getState
        argumentType <- argument.infer(context)
        if applicand is Function(_, isCase, domain, codomain, body)
            if not isCase and isMetaVariable(domain)
                # suppport polymorphism in nested functions
                metacontext' <- getState
                scheme := argumentType.generalize(metacontext', metacontext)
                context' := context.pushContext(scheme, argument)
                body.infer(context')
            pass
        applicandType <- applicand.infer(context)
        returnType <- newMetaVariable(tag)
        applicandTag := getTermTag(applicand)
        functionType := Implication(applicandTag, argumentType, returnType)
        _ <- unify(resolve(Closure(applicandType, environment)),
                  resolve(Closure(functionType, environment)))
        lift returnType
    case Construction(tag, parameters, objectType, signatures)
        _ <- signatures.mapState(infer(context))
        lift Universe(tag, 0)
    case Element(tag, type, term)
        # type should always be a closed term, so this should be safe
        peelMetaFunctions(type)
    case Numeral(tag, type, value)
        lift type
    case Operation(tag, term, _)
        term.infer(context)


def getFixScheme(tag)
    syntax(=>>) := syntax(++)
    (=>>) := Implication(tag)
    a := MetaVariable(veil(tag), 0)
    Scheme([a], (a =>> a) =>> a)


def normalize(term)
    # todo: evaluateTerm will return Unknown for variables inside binders
    resolve(evaluateTerm(term)).mapComponents(normalize)


def inferType(schemes, Binding(tag, closure @ Closure(term, environment)))
    if getTagLexeme(tag) =*= "fix"
        schemes.push(getFixScheme(tag))
    metacontext := newArray([])
    context := (environment, schemes)
    (type, metacontext') := infer(context, term, metacontext)
    type' := normalize(resolve(Closure(type, environment)))
    schemes.push(type'.generalize(metacontext', metacontext))


def main(input)
    bindings := reverse(parse(getBuiltin, input))
    schemes := bindings.cascade(inferType, newArray([]))
    tags := bindings.map(getBindingTag)
    showTypings(zip(tags, schemes.toList))

#*
