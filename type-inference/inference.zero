#* inference.zero

def pushContext(term, scheme, context @ (terms, schemes))
    # TODO: replace schemes with an array of closures of types
    (terms.push(Closure(term, terms)), schemes.push(scheme))


def pushParameter(scheme, context)
    # TODO: term should be an atom
    pushContext(Top(noTag), scheme, context)


def getAllFreeVariables(metacontext, metacontext')
    indices := (0 ...)[0, metacontext.getSize]
    join(indices.map(MetaVariable(noTag)).map(
        getFreeVariables(metacontext'))).deduplicateBy(sameHead)


def generalize(metacontext, oldMetacontext, type)
    syntax(\\) := syntax(\)
    vs \\ vs' := vs |: (v -> not vs'.any(sameHead(v)))
    type' := type.substitute(metacontext)
    universals := type'.getFreeVariables(metacontext) \\
        metacontext.getAllFreeVariables(oldMetacontext)
    Scheme(universals, type')


def newInstantiationMapping(universals, metacontext)
    tags := universals.map(getTermTag)
    (metaVariables, metacontext') := newMetaVariables(tags, metacontext)
    addresses := universals.map(maybeAddress).prune
    mapping := newMapping(zip(addresses, metaVariables))
    (mapping, metacontext')


def instantiate(tag, metacontext, Scheme(universals, type))
    (mapping, metacontext') := newInstantiationMapping(universals, metacontext)
    (type.applyMapping(mapping).setLocation(tag), metacontext')


def peelImplicits(type)
    if type is Implicit(tag, _, body)
        (tags, type') := peelImplicits(body)
        (tag :: tags, type')
    ([], type)


def convertToScheme(metacontext, type)
    # type may be wrapped in some implicits; we create a new metavariable
    # for each implicit, then substitute them in the type
    (tags, type') := peelImplicits(type)
    (metaVariables, metacontext') := newMetaVariables(tags, metacontext)
    environment := newArray(metaVariables.map(v -> Closure(v, newArray([]))))
    type'' := flattenClosure(Closure(type', environment))
    (Scheme(metaVariables, type''), metacontext')


def instantiateType(metacontext, type)
    convertToScheme(metacontext, type).mapFirst(getType)


def getInnerType(tag, outerType, environment)
    _ @ Closure(construction, environment') :=
        evaluateClosure(Closure(outerType, environment))
    if construction is Construction(_, _, type)
        flattenClosure(Closure(type, environment'))
    throwError("expected construction in case annotation", tag)


def instantiateDomain(domain, metacontext)
    if isMetaVariable(domain)
        metacontext.newMetaVariable(getTermTag(domain))
    instantiateType(metacontext, domain)


def unifyCodomain(codomain, bodyType, context, metacontext)
    if isMetaVariable(codomain)
        (bodyType, metacontext)
    metacontext' := unify(context, metacontext, (codomain, bodyType))
    if getSize(metacontext') < 0  # todo: remove this when unify returns Either
        abort("force unification errors")
    (codomain, metacontext')


def infer(context @ (environment, types), metacontext)
    case Top(tag)
        (Top(tag), metacontext)
    case Constant(tag, index)
        scheme := types.get(index) ?? throwError("unbound constant", tag)
        scheme.instantiate(tag, metacontext)
    case Variable(tag, debruijn)
        scheme := types.peek(debruijn -- 1) ??
            throwError("unbound variable", tag)
        scheme.instantiate(tag, metacontext)
    case MetaVariable(tag, address)
        if metacontext.get(address) ?? Void is Just(Closure(term, _))
            infer(context, metacontext, term)
        newMetaVariable(tag, metacontext)
    case Function(tag, isCase, domain, codomain, body)
        # a metavariable in the domain field means there is no annotation
        (domain, metacontext) := instantiateDomain(domain, metacontext)
        parameterType := if isCase then
            getInnerType(tag, domain, environment) else domain
        # todo: type may contain variables which would get lost here
        (scheme, metacontext) := convertToScheme(metacontext, parameterType)
        context' := context.pushParameter(scheme)
        (bodyType, metacontext) := infer(context', metacontext, body)
        (codomain, metacontext) :=
            unifyCodomain(codomain, bodyType, context, metacontext)
        (Implication(tag, domain, codomain), metacontext)
    case Implication(tag, type, body)
        (type, metacontext) := if isMetaVariable(type) then
            metacontext.newMetaVariable(tag) else (type, metacontext)
        context' := context.pushParameter(Scheme([], type))
        infer(context', metacontext, body).mapFirst(_ -> Top(veil(tag)))
    case Implicit(tag, type, body)
        (type, metacontext) := if isMetaVariable(type) then
            metacontext.newMetaVariable(tag) else (type, metacontext)
        context' := context.pushParameter(Scheme([], type))
        infer(context', metacontext, body)
    case Application(tag, applicand, argument)
        (argumentType, metacontext') := infer(context, metacontext, argument)
        if applicand is Function(_, isCase, domain, codomain, body)
            if not isCase and isMetaVariable(domain)
                # suppport polymorphism in nested functions
                scheme := argumentType.generalize(metacontext', metacontext)
                context' := context.pushParameter(scheme)
                infer(context', metacontext', body)
            pass
        (applicandType, metacontext'') :=
            infer(context, metacontext', applicand)
        (returnType, metacontext''') := metacontext''.newMetaVariable(tag)
        applicandTag := getTermTag(applicand)
        functionType := Implication(applicandTag, argumentType, returnType)
        (returnType, unify(context, metacontext''',
            (applicandType, functionType)))
    case Construction(tag, parameters, objectType)
        (Top(veil(tag)), metacontext)
    case Element(tag, type, term)
        instantiateType(metacontext, type)
    case Numeral(tag, type, value)
        (type, metacontext)
    case Operation(tag, type, _, _)
        instantiateType(metacontext, type)


def getFixScheme(tag)
    syntax(>->) := syntax(++)
    (>->) := Implication(tag)
    a := MetaVariable(veil(tag), 0)
    Scheme([a], (a >-> a) >-> a)


def inferType(context, (tag, term))
    if getTagLexeme(tag) =*= "fix"
        context.pushContext(term, getFixScheme(getTermTag(term)))
    metacontext := newArray([])
    (type, metacontext') := infer(context, metacontext, term)
    scheme := type.generalize(metacontext', metacontext)
    context.pushContext(term, scheme)


def main(input)
    (tags, _, environment) := parse(getBuiltin, input)
    terms := environment.toList.map(getTerm)
    context := (newArray([]), newArray([]))
    context' := zip(tags, terms).cascade(inferType, context)
    schemes := second(context').toList
    showTypings(zip(tags, schemes))

#*
