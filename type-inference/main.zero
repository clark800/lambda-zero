#* main.zero

def getFixType(tag)
    syntax(=>>) := syntax(++)
    tag := renameTag(tag, "a", NOFIX)
    (=>>) := Implication(veil(tag))
    a1 := Variable(tag, +_ 1)
    a2 := Variable(tag, +_ 2)
    Intersection(tag, Type(veil(tag), +_ 0), (a1 =>> a2) =>> a2)


def renameUniversals(closure)
    (either, metacontext) := instantiate(closure, emptyMetacontext)
    try closure' := either
    return enclose(closure'.generalize(metacontext))


def getType(types, Binding(tag, closure, annotation, isConstruct))
    if getTagLexeme(tag) =*= "fix"
        lift return enclose(getFixType(tag))
    _ @ Closure(term, environment) := closure
    context := (environment, types)
    if annotation is Just(type)
        try _ <- getTerm(type).inferType(context) # typecheck the type
        if isConstruct  # skip check for constructors and deconstructors
            lift renameUniversals(type)
        try _ <- check(context, type, term)
        lift return type
    inferGeneralizedType(context, term)


def pushType(types, binding)
    try type := first(getType(types, binding, emptyMetacontext))
    return types.push(type)


def showTyping((tag, type))
    showTag(tag) ++ " : " ++ showClosure(type)


def showTypings(typings)
    typings.map(showTyping).joinWith("\n") ++ "\n"


def checkedCascade(f, z)
    case [] -> (z, Void)
    case x :: xs'
        match f(z, x)
            case Left(y) -> (z, Just(y))
            case Right(z') -> xs'.checkedCascade(f, z')


def showTypeError
    case Void -> ""
    case Just(message) -> "\n\n" ++ message ++ "\n"


def main(input)
    bindings := reverse(parse(getBuiltin, input))
    (types, maybeError) := bindings.checkedCascade(pushType, newArray([]))
    tags := bindings.map(getBindingTag)
    showTypings(zip(tags, types.toList)) ++ showTypeError(maybeError)

#*
