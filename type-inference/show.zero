#* show.zero

define showTypeLocation(type)
    showLocation(getTagLocation(getTypeTag(type)))

define showTypeAndLocation(type)
    showType(type) ++ " at " ++ showTypeLocation(type)

define showMaybeType(maybeType)
    with maybeType as Just(type)
        showType(type)
    "Free"

define showVariables(variables)
    variables.dump.map((address, maybeValue) ->
        getVariableName(address) ++ " := " ++
        showMaybeType(maybeValue)).joinWith("\n")

define addRenaming((renaming, variables), type)
    with type as TypeVariable(source, address)
        if renaming.hasKey(address)
            (renaming, variables)
        (type', variables') := variables.newTypeVariable(source)
        (renaming.set(address, type'), variables')
    getComponentTypes(type).cascade(addRenaming, (renaming, variables))

define makeRenaming(type)
    renaming := newRenaming([])
    variables := newStore([])
    first(addRenaming((renaming, variables), type))

define showTypeWithRenaming(type, renaming)
    showType(type.rename(renaming))

define showUniversals(universals, renaming)
    universals.map(universal ->
        "âˆ€" ++ showTypeWithRenaming(universal, renaming) ++ " ").join

define showScheme(Scheme(universals, type))
    renaming := makeRenaming(type)
    showUniversals(universals, renaming) ++ showTypeWithRenaming(type, renaming)

define showTyping((tag, scheme))
    getTagLexeme(tag) ++ " : " ++ showScheme(scheme)

define showTypings(typings)
    typings.map(showTyping).joinWith("\n")

#*
