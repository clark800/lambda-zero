#* show.zero

define showTypeLocation(type)
    if isFreeType(type)
        "VOID"
    showLocation(getTagLocation(getTermTag(getSource(type))))

define showSource(type)
    if isFreeType(type) then "VOID" else showTerm(getSource(type))

define showTypeAndLocation(type)
    showType(type) ++ (if isNoTerm(getSource(type)) then "" else
        " at " ++ showTypeLocation(type))

define showVariables(variables)
    variables.getDump.map((address, value) ->
        getVariableName(address) ++ " := " ++ showType(value)).joinWith("\n")

define addRenaming((renaming, variables), type)
    if isTypeVariable(type)
        if renaming.hasKey(type)
            (renaming, variables)
        (type', variables') := variables.newTypeVariable(getSource(type))
        (renaming.set(type, type'), variables')
    getComponentTypes(type).cascade(addRenaming, (renaming, variables))

define makeRenaming(type)
    renaming := newRenaming([])
    variables := newStore
    first(addRenaming((renaming, variables), type))

define showTypeWithRenaming(type, renaming)
    showType(type.rename(renaming))

define showUniversals(universals, renaming)
    universals.map(universal ->
        "âˆ€" ++ showTypeWithRenaming(universal, renaming) ++ " ").join

define showScheme(Scheme(universals, type))
    renaming := makeRenaming(type)
    showUniversals(universals, renaming) ++ showTypeWithRenaming(type, renaming)

define showTyping((tag, scheme))
    getTagLexeme(tag) ++ " : " ++ showScheme(scheme)

define showTypings(typings)
    typings.map(showTyping).joinWith("\n")

#*
