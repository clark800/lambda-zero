#* substitute.zero

def newMetaVariable(tag, metacontext)
    (address, metacontext') := metacontext.append(Void)
    (MetaVariable(tag, address), metacontext')


newMetaVariables(tags, metacontext) :=
    tags.cascadeMap(newMetaVariable, metacontext)


newMapping(mappings) := newTable((<=), mappings)


def applyMapping(mapping, type)
    with type as MetaVariable(_, address)
        mapping.lookup(address) ?? type
    type.mapComponents(applyMapping(mapping))


def getFreeVariables(metacontext, type)
    with type as MetaVariable(_, address)
        with metacontext.get(address) ?? Void as Just(closure)
            getFreeVariables(metacontext, flattenClosure(closure))
        [type]
    join(getComponents(type).map(getFreeVariables(metacontext))).
        deduplicateBy(sameHead)


def showMaybeClosure(maybeClosure)
    with maybeClosure as Just(Closure(type, _))
        showTerm(type)
    "Free"


def showMetacontext(metacontext)
    zip(0 ..., metacontext.toList).map((address, maybeClosure) ->
        showMetaVariable(address) ++ " := " ++
        showMaybeClosure(maybeClosure)).joinWith("\n")


def recursionError(type, type', metacontext)
    abort("Type error: recursive type at " ++
        showTermLocation(type) ++ " and " ++ showTermLocation(type') ++
        ":\n" ++ showTerm(type) ++ " = " ++ showTerm(type') ++ "\n\n" ++
        showMetacontext(metacontext))


def substitute(metacontext, type)
    with type as MetaVariable(tag, address)
        with metacontext.get(address) ?? Void as Just(closure)
            type' := flattenClosure(closure)
            type'' := type'.setLocation(tag)
            if type''.getFreeVariables(newArray([])).any(sameHead(type))
                recursionError(type, type, metacontext)
            type''.substitute(metacontext)
        type
    type.mapComponents(substitute(metacontext))

#*
