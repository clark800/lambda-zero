#* types.zero

define getSource
    FreeType -> undefined
    BottomType(source) -> source
    TypeVariable(source, _) -> source
    FunctionType(source, _, _) -> source
    ADT(source, _, _, _, _) -> source


define setSource(type, source)
    match type
        FreeType -> FreeType
        BottomType(_) -> BottomType(source)
        TypeVariable(_, index) -> TypeVariable(source, index)
        FunctionType(_, parameter, body) ->
            FunctionType(source, parameter, body)
        ADT(_, name, parameters, count, index) ->
            ADT(source, name, parameters, count, index)


define indexType
    FreeType -> 0
    BottomType(_) -> 1
    TypeVariable(_, _) -> 2
    FunctionType(_, _, _) -> 3
    ADT(_, _, _, _, _) -> 4


isFreeType(type) := indexType(type) = 0
isBottomType(type) := indexType(type) = 1
isTypeVariable(type) := indexType(type) = 2
isFunctionType(type) := indexType(type) = 3
isADT(type) := indexType(type) = 4


define getVariableName(index)
    characters := ['a', 'b', 'c', 'd']
    if index < 4
        [characters[index]]
    getVariableName((index // 4) - 1) ++ [characters[index % 4]]


define getSuffix(count, index)
    if count <= 1 or index = 0 then ""
    subscripts := ["₀", "₁", "₂", "₃", "₄", "₅", "₆", "₇", "₈", "₉"]
    showNatural(index).map(x -> x - '0').map(x -> subscripts[x]).join


define showType
    case FreeType; "_"
    case BottomType(_); "⊥"
    case TypeVariable(_, index); getVariableName(index)
    case FunctionType(_, parameter, body)
        if isFunctionType(parameter)
            "(" ++ showType(parameter) ++ ")" ++ " → " ++ showType(body)
        showType(parameter) ++ " → " ++ showType(body)
    case ADT(_, name, parameters, count, index)
        suffix := getSuffix(count, index)
        if isNil(parameters) then name ++ suffix else name ++ "(" ++
            parameters.map(showType).joinWith(", ") ++ ")" ++ suffix


define getComponentTypes
    FreeType -> []
    BottomType(_) -> []
    TypeVariable(_, _) -> []
    FunctionType(_, parameter, body) -> [parameter, body]
    ADT(_, _, parameters, _, _) -> parameters


syntax(=~=) := syntax(=)

define a =~= b
    match a
        FreeType -> undefined
        BottomType(_) -> isBottomType(b)
        TypeVariable(_, _) -> isTypeVariable(b) and
            getTypeVariableIndex(a) = getTypeVariableIndex(b)
        FunctionType(_, _, _) -> isFunctionType(b) and
            getParameterType(a) =~= getParameterType(b) and
            getBodyType(a) =~= getBodyType(b)
        ADT(_, _, _, _, _) -> isADT(b) and getName(a) =*= getName(b) and
            all(zipWith(getComponentTypes(a), (=~=), getComponentTypes(b)), id)


define newTypeVariable(variables, source)
    type := TypeVariable(source, cardinality(variables))
    (type, variables.insert(type, FreeType))

newTypeVariables(originals, variables) :=
    originals.map(getSource).cascadeMap(`newTypeVariable, variables)

isFree(type, variables) := not variables.hasKey(type) or
    isFreeType(variables.get(type))

define mapComponents(type, f)
    match type
        FreeType -> type
        BottomType(_) -> type
        TypeVariable(_, _) -> type
        FunctionType(source, parameter, body) ->
            FunctionType(source, f(parameter), f(body))
        ADT(source, name, parameters, count, index) ->
            ADT(source, name, parameters.map(f), count, index)

newVariableMap(mappings) := newTable((<=).on(getTypeVariableIndex), mappings)
empty := newVariableMap([])

define rename(type, variables)
    if isTypeVariable(type)
        variables.lookup(type) ?? type
    type.mapComponents(`rename(variables))

#*
