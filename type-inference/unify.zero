#* unify.zero

def unificationError(originals, left, right)
    abort("Type error: Can't unify\n" ++
        showTerm(left) ++ "\nwith\n" ++ showTerm(right) ++
        "\nwhile trying to unify\n" ++
        showTermAndLocation(first(originals)) ++
        "\nwith\n" ++
        showTermAndLocation(second(originals)))


def unifyFailure(originals, metacontext, (left, right))
    unificationError(originals.mapPair(substitute(metacontext)),
        left.substitute(metacontext), right.substitute(metacontext))


def isFree(metavariable, metacontext)
    if metavariable is MetaVariable(_, address)
        isVoid(metacontext.get(address) ?? Void)
    False


def getValue(type, metacontext)
    if type is MetaVariable(_, address)
        if metacontext.get(address) ?? Void is Just(term)
            term
        pass
    throwError("internal error", getTermTag(type))


def substituteAndEvaluate(metacontext, term)
    resolve(evaluateClosure(enclose(substitute(metacontext, term))))


def insertMetaVariable(term, metacontext)
    tag := getTermTag(term)
    (metavariable, metacontext') := newMetaVariable(tag, metacontext)
    closure' := Closure(term, newArray([enclose(metavariable)]))
    (resolve(closure'), metacontext')


# Robinson's Unification Algorithm
# see Comparing Unification Algorithms in First-Order Theorem Proving
# by Krystof Hoder and Andrei Voronkov
def unify'(fail, metacontext, (left, right))
    left := substituteAndEvaluate(metacontext, left)
    right := substituteAndEvaluate(metacontext, right)

    if isUnknown(left) or isUnknown(right)
        fail(metacontext, (left, right))
    if left is MetaFunction(tag, type, body)
        (left', metacontext') := insertMetaVariable(body, metacontext)
        unify'(fail, metacontext', (left', right))
    if right is MetaFunction(tag, type, body)
        (right', metacontext') := insertMetaVariable(body, metacontext)
        unify'(fail, metacontext', (left, right'))
    if isMetaVariable(left) and not isFree(left, metacontext)
        unify'(fail, metacontext, (metacontext.getValue(left), right))
    if isMetaVariable(right) and not isFree(right, metacontext)
        unify'(fail, metacontext, (left, metacontext.getValue(right)))
    if isSameHead(left, right)
        zip(getComponents(left), getComponents(right)).
            cascade(unify'(fail), metacontext)
    if left is MetaVariable(_, address)
        metacontext.set(address, Just(right))
    if right is MetaVariable(_, address)
        metacontext.set(address, Just(left))
    fail(metacontext, (left, right))


def unify(left, right, metacontext)
    types := (left, right)
    metacontext' := unify'(unifyFailure(types), metacontext, types)
    ((), metacontext')

#*
