getBuiltin(builtin) := (
    maxInteger := 9223372036854775807

    getInteger(term) := (
        isInteger(term) ? getData(term)
        runtimeError("expected integer argument to", builtin)
    )

    compute(f, left, right) :=
        f(getInteger(getTerm(left)), getInteger(getTerm(right)))

    makeIntegerBuiltin(f, newType, left, right) :=
        newClosure(newType(getTag(builtin), f.compute(left, right)), [])

    addBuiltin(left, right) := (
        left > 0 /\ right > 0 /\ left > maxInteger - right ?
            runtimeError("integer overflow in", builtin)
        left < 0 /\ right < 0 /\ left < -maxInteger - right ?
            runtimeError("integer overflow in", builtin)
        left + right
    )

    subtractBuiltin(left, right) := (
        left > 0 /\ right < 0 /\ left > maxInteger + right ?
            runtimeError("integer overflow in", builtin)
        left < 0 /\ right > 0 /\ left < -maxInteger + right ?
            runtimeError("integer overflow in", builtin)
        left - right
    )

    multiplyBuiltin(left, right) := (
        right != 0 /\ abs(left) > abs(maxInteger / right) ?
            runtimeError("integer overflow in", builtin)
        left * right
    )

    divideBuiltin(left, right) := (
        right = 0 ? runtimeError("divide by zero in", builtin)
        left / right
    )

    moduloBuiltin(left, right) := (
        right = 0 ? runtimeError("divide by zero in", builtin)
        left % right
    )

    builtins := newDictionary([
        ("+", (2, makeIntegerBuiltin(addBuiltin, newInteger))),
        ("-", (2, makeIntegerBuiltin(subtractBuiltin, newInteger))),
        ("*", (2, makeIntegerBuiltin(multiplyBuiltin, newInteger))),
        ("/", (2, makeIntegerBuiltin(divideBuiltin, newInteger))),
        ("%", (2, makeIntegerBuiltin(moduloBuiltin, newInteger))),
        ("=", (2, makeIntegerBuiltin((=), newBoolean))),
        ("!=", (2, makeIntegerBuiltin((!=), newBoolean))),
        ("<", (2, makeIntegerBuiltin((<), newBoolean))),
        (">", (2, makeIntegerBuiltin((>), newBoolean))),
        ("<=", (2, makeIntegerBuiltin((<=), newBoolean))),
        (">=", (2, makeIntegerBuiltin((>=), newBoolean))),
        ("error", (1, closure -> error(extractString(getTerm(closure)))))
    ])
    builtins.lookup(getLexeme(builtin))
)
